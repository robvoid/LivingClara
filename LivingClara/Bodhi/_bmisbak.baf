
//Baldur scripts for Bodhi turning into mist on death
IF
	!InParty("_BBodhi")
	Global("_BBodhiMist","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobalTimer("_BBodhiDieTimer","GLOBAL",EIGHT_HOURS)
	ActionOverride("_BBodhi",ApplySpell(Myself,HEAL_NO_VISUAL))
	SetGlobal("_BBodhiMist","GLOBAL",3)
END

IF
	Global("_BBodhiMist","GLOBAL",3)
	GlobalTimerExpired("_BBodhiDieTimer","GLOBAL")
		OR(6)
	!AreaType(OUTDOOR)
	AreaCheck("ar1404") //Shadow Temple Land shadowed (SoA)
	AreaCheck("AR5202") //Forest of Mir (ToB)
	TimeLT(DAWN_END)
	TimeGT(DUSK_START)
	HasItemEquiped("ohhcloak","_BBodhi")
THEN
	RESPONSE #100
	MoveGlobalObjectOffScreen("_BBodhi",Player1)
	SetGlobal("_BBodhiMist","GLOBAL",4)
END

IF
	Global("_BBodhiMist","GLOBAL",4)
THEN
	RESPONSE #100
	SetGlobalTimer("_BBodhiDieFixTimer","GLOBAL",FIVE_MINUTES)
	SetGlobal("_BBodhiMist","GLOBAL",5)
END

IF
	Global("_BBodhiMist","GLOBAL",5)
THEN
	RESPONSE #100
	ActionOverride("_BBodhi",MoveToObject(Player1))
	SetGlobal("_BBodhiMist","GLOBAL",6)
END

IF
	Global("_BBodhiMist","GLOBAL",6)
	GlobalTimerExpired("_BBodhiDieFixTimer","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_BBodhiMist","GLOBAL",7)
END

IF
	Global("_BBodhiMist","GLOBAL",7)
THEN
	RESPONSE #100
	ActionOverride("_BBodhi",SetInterrupt(TRUE))
	MoveGlobalObjectOffScreen("_BBodhi",Player1)
	SetGlobal("_BBodhiMist","GLOBAL",4)
END


//Just in case Bodhi somehow dies. This should restart her.
IF
	!Global("_BBodhiMist","GLOBAL",2)
	StateCheck("_bBodhi",STATE_REALLY_DEAD)
THEN
	RESPONSE #100
	ActionOverride("_BBodhi",ApplySpell(Myself,CLERIC_RESURRECTION))
	ActionOverride("_BBodhi",ApplySpellRES("_bbomi25",Myself))
	ActionOverride("_BBodhi",TransformItem("_bbohair","_bbohar2"))
	SetGlobal("_BBodhiMist","GLOBAL",2) 
	SetGlobal("_BBodhiRemoved","GLOBAL",2) 
	ActionOverride("_BBodhi",LeaveParty())
	ActionOverride("_BBodhi",EscapeAreaMove("AR4000",771,931,S))
END

//Baldur scripts for Bodhi leaving if it's sunny.
IF
	!InParty("_BBodhi")
	Global("_BBodhiBurns","GLOBAL",2)
		OR(6)
	!AreaType(OUTDOOR)
	AreaCheck("ar1404") //Shadow Temple Land shadowed (SoA)
	AreaCheck("AR5202") //Forest of Mir (ToB)
	TimeLT(DAWN_END)
	TimeGT(DUSK_START)
	HasItemEquiped("ohhcloak","_BBodhi")
THEN
	RESPONSE #100
	MoveGlobalObjectOffScreen("_BBodhi",Player1)
	SetGlobal("_BBodhiBurns","GLOBAL",3)
END

IF
	Global("_BBodhiBurns","GLOBAL",3)
THEN
	RESPONSE #100
	SetGlobalTimer("_BBodhiBurnsFixTimer","GLOBAL",FIVE_MINUTES)
	SetGlobal("_BBodhiBurns","GLOBAL",4)
END

IF
	Global("_BBodhiBurns","GLOBAL",4)
THEN
	RESPONSE #100
	ActionOverride("_BBodhi",MoveToObject(Player1))
	SetGlobal("_BBodhiBurns","GLOBAL",5)
END

IF
	Global("_BBodhiBurns","GLOBAL",5)
	GlobalTimerExpired("_BBodhiBurnsFixTimer","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_BBodhiBurns","GLOBAL",6)
END

IF
	Global("_BBodhiBurns","GLOBAL",6)
THEN
	RESPONSE #100
	ActionOverride("_BBodhi",SetInterrupt(TRUE))
	MoveGlobalObjectOffScreen("_BBodhi",Player1)
	SetGlobal("_BBodhiBurns","GLOBAL",3)
END
	


