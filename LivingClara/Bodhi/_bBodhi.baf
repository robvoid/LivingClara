//SOA Scripts

//Script for turning into mist and leaving instead of dying.
IF
	HPLT(Myself,10)
	Global("_BBodhiMist","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_BBodhiMist","GLOBAL",1)
END

IF
	Global("_BBodhiMist","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_BBodhiMist","GLOBAL",6)
	Range([PC],3)
THEN
	RESPONSE #100
	TransformItem("_bbohar2","_bbohair")
	SetDialog("_bBodhip")
	SetGlobal("_BBodhiMist","GLOBAL",8)
END

IF
	Global("_BBodhiMist","GLOBAL",8)
	THEN
		RESPONSE #100
	StartDialogNoSet(Player1)
END

//Scripts for leaving if it's sunny
IF
	AreaType(OUTDOOR)
	!AreaCheck("ar1404") //Shadow Temple Land shadowed (SoA)
	TimeGT(DAWN_END)
	TimeLT(DUSK_START)
	!HasItemEquiped("ohhcloak",Myself)  // Cloak of Dragomir
	Global("_BBodhiBurns","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_BBodhiBurns","GLOBAL",1)
	CreateVisualEffectObject("SPSMKJET",Myself)
END

IF
	Global("_BBodhiBurns","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_BBodhiBurns","GLOBAL",5)
	Range([PC],3)
THEN
	RESPONSE #100
	TransformItem("_bbohar2","_bbohair")
	SetDialog("_bBodhip")
	SetGlobal("_BBodhiBurns","GLOBAL",7)
END

IF
	Global("_BBodhiBurns","GLOBAL",7)
	THEN
		RESPONSE #100
	StartDialogNoSet(Player1)
END

//Script for First Hexxat Cloak Talk, next script in Hexxat
IF
	InParty("_BBodhi")
	InParty("HEXXAT")
	HasItemEquiped("ohhcloak","_bBodhi")
	!HasItem("ohhcloak","Hexxat")
	Global("_bCloakTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bCloakTalk","GLOBAL",1)
END

//Script for 2nd Hexxat Cloak Talk
IF
	InParty("_BBodhi")
	InParty("HEXXAT")
	HasItemEquiped("ohhcloak","_bBodhi")
	HasItemEquiped("ohhcloak","Hexxat")
	OR(2)
	Global("_bCloakTalk","GLOBAL",0)
	Global("_bCloakTalk","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bCloakTalk","GLOBAL",3)
END

IF
	Global("_bCloakTalk","GLOBAL",3)
	InParty("_BBodhi")
	!StateCheck("_BBodhi",CD_STATE_NOTVALID)
	InParty("HEXXAT")
	!StateCheck("HEXXAT",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Script for giving Bodhi the Rhynn Lanthorn
IF
	!HasItem("c6lantho",Myself)
	Global("_bBodhiGetsLanthorn","GLOBAL",0)
THEN
	RESPONSE #100
	GiveItemCreate("c6lantho",Myself,0,0,0)  // Lanthorn
	SetGlobal("_bBodhiGetsLanthorn","GLOBAL",1)
	Continue()
END

//Script for giving the Cloak of Dragomir to Bodhi
IF
	Global("_bBodhihasCloakofDragomir","GLOBAL",1)
	!HasItem("OHHCLOAK",Myself)
	Global("_bBodhigetsCloakofDragomir","GLOBAL",0)
THEN
	RESPONSE #100
	GiveItemCreate("OHHCLOAK",Myself,0,0,0)
	SetGlobal("_bBodhigetsCloakofDragomir","GLOBAL",1)
	Continue()
END

//Script once combat ends to make the scripts set as if she's dead.
IF
	InParty("_bBodhi")
	Global("_bWhenBodhiFirstJoinsSoA","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	EraseJournalEntry(16351)
	EraseJournalEntry(7002)
	EraseJournalEntry(3716)
	EraseJournalEntry(5814)
	EraseJournalEntry(16331)
	EraseJournalEntry(15710)
	EraseJournalEntry(15784)
	EraseJournalEntry(7090)
	EraseJournalEntry(7236)
	EraseJournalEntry(7683)
	EraseJournalEntry(7737)
	EraseJournalEntry(8302)
	EraseJournalEntry(10269)
	EraseJournalEntry(11771)
	SetGlobal("udMind","GLOBAL",41)
	SetGlobal("_bWhenBodhiFirstJoinsSoA","GLOBAL",1)
	SetGlobal("Coffin","AR0809",2)  // Bodhi's Lair (Abduction Plot, getting Imoen's soul)
	SetGlobal("BodhiDead","GLOBAL",1)
	AddXPObject(Player1,48500)
	AddXPObject(Player2,48500)
	AddXPObject(Player3,48500)
	AddXPObject(Player4,48500)
	AddXPObject(Player5,48500)
	AddXPObject(Player6,48500)
	EraseJournalEntry(97342)  // I must find the Rhynn Lanthorn, which will open the way to Suldanessellar, where Irenicus awaits. I am told that the Rhynn Lanthorn is currently possessed by Bodhi in her lair in Athkatla's Graveyard District. If I find any allies before then, they may be of help in facing her.
	AddJournalEntry(97342,QUEST_DONE)  // I must find the Rhynn Lanthorn, which will open the way to Suldanessellar, where Irenicus awaits. I am told that the Rhynn Lanthorn is currently possessed by Bodhi in her lair in Athkatla's Graveyard District. If I find any allies before then, they may be of help in facing her.
END

//Script to interject into the Knights of the Noble Order arriving line
IF
	IfValidForPartyDialog("_bBodhi")
	See("NobleOrder")
	Global("_bOrderAttacksCN","GLOBAL",2)
	Global("_bBodhiInterjectsNobleOrder","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bBodhiInterjectsNobleOrder","GLOBAL",1)
END

IF
	Global("_bBodhiInterjectsNobleOrder","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Backup trigger for Bodhi being in your party.
IF
	!InParty("_bBodhi")
	Global("_bBodhiJoinsParty","GLOBAL",2)
	ActuallyInCombat()
	AreaCheck("AR0809")
THEN
	RESPONSE #100
	ClearActions(Myself)
	JoinParty()
END

//Script to trigger Bodhi's dialogue immediately following combat if she had turned your love into a vampire.
IF
	IfValidForPartyDialog("_bBodhi")
	Dead("vampano")
	AreaCheck("AR0809")
	CombatCounter(0)
	Global("_bBodhiLoverDeadMale","GLOBAL",0)
	Global("_bBodhiLoverDeadTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bBodhiLoverDeadMale","GLOBAL",1)
	SetGlobal("_bBodhiLoverDeadTalk","GLOBAL",1)
END

IF
	IfValidForPartyDialog("_bBodhi")
	OR(3)
		Dead("vampaer")
		Dead("vampjah")
		Dead("vampvic")
	AreaCheck("AR0809") //Bodhi's lair (when you battle her)
	CombatCounter(0)
	Global("_bBodhiLoverDeadFemale","GLOBAL",0)
	Global("_bBodhiLoverDeadTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bBodhiLoverDeadFemale","GLOBAL",1)
	SetGlobal("_bBodhiLoverDeadTalk","GLOBAL",1)
END

IF
	Global("_bBodhiLoverDeadTalk","GLOBAL",1)
	OR(2)
		Global("_bBodhiLoverDeadFemale","GLOBAL",1)
		Global("_bBodhiLoverDeadMale","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Second Dialogue once you get the books.
IF
	Global("_bBodhiLoverDeadTalk","GLOBAL",2)
	IfValidForPartyDialog("_bBodhi")
	PartyHasItem("BOOK94")
	PartyHasItem("BOOK95")
	PartyHasItem("BOOK96")
THEN
	RESPONSE #100
	SetGlobal("_bBodhiLoverDeadTalk","GLOBAL",3)
END

IF
	Global("_bBodhiLoverDeadTalk","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Dialogue once you go back upstairs in her hideout.
IF
	IfValidForPartyDialog("_bBodhi")
	AreaCheck("AR0808") //Bodhi's Hideout (when you battle her)
	Global("_bBodhiHideoutElhanTalk","GLOBAL",0)
	
THEN
	RESPONSE #100
	SetGlobal("_bBodhiHideoutElhanTalk","GLOBAL",1)
END

IF
	Global("_bBodhiHideoutElhanTalk","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0) 
	PlaySound("_bBoMu01")
	StartDialogNoSet(Player1)
END
	
//Script if you have sex.
IF
	IfValidForPartyDialog("_bBodhi")
	Global("_bBodhiVampireSex","GLOBAL",1)
	Global("_bBodhiPostVampireSexTalk","GLOBAL",0)
	AreaCheck("AR0808") //Bodhi's Hideout (when you battle her)
THEN
	RESPONSE #100
	StartMovie("restdung")
	DayNight(MIDNIGHT)
	SetGlobal("_bBodhiPostVampireSexTalk","GLOBAL",1)
END

IF
	Global("_bBodhiPostVampireSexTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
