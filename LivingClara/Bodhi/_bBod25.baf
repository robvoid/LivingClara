// Bodhi's TOB scripts

//Bodhi Elf Script
IF
	Global("_bBodhiBecomesElf","GLOBAL",1)
	Global("_bBodhiVampireVanish","GLOBAL",0)
THEN
	RESPONSE #100
	DropInventory()
	CreateCreature("_bBodElf",[743.427],S)
	SetGlobal("_bBodhiVampireVanish","GLOBAL",1)
	LeaveParty()
	Continue()
END

IF
	Global("_bBodhiVampireVanish","GLOBAL",1)
THEN
	RESPONSE #100
	MoveBetweenAreas("AR0810",[383.479],S)
	SetGlobal("_bBodhiVampireVanish","GLOBAL",2)
END	

//Script for summoning.
IF
Global("_bBodhiSummoned","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bBodhiSummoned","GLOBAL",2)
END

IF
Global("_bBodhiSummoned","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Script for turning into mist and leaving instead of dying.
IF
	HPLT(Myself,10)
	Global("_BBodhiMist","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_BBodhiMist","GLOBAL",1)
END

IF
	Global("_BBodhiMist","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_BBodhiMist","GLOBAL",6)
	Range([PC],3)
THEN
	RESPONSE #100
	TransformItem("_bbohar2","_bbohair")
	SetDialog("_bBod25p")
	SetGlobal("_BBodhiMist","GLOBAL",8)
END

IF
	Global("_BBodhiMist","GLOBAL",8)
	THEN
		RESPONSE #100
	StartDialogNoSet(Player1)
END

//Scritps for leaving if it's sunny
IF
	AreaType(OUTDOOR)
	!AreaCheck("ar1404") //Shadow Temple Land shadowed (SoA)
	!AreaCheck("AR5202") //Forest of Mir (ToB)
	TimeGT(DAWN_END)
	TimeLT(DUSK_START)
	!HasItemEquiped("ohhcloak",Myself)  // Cloak of Dragomir
	Global("_BBodhiBurns","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_BBodhiBurns","GLOBAL",1)
	CreateVisualEffectObject("SPSMKJET",Myself)
END

IF
	Global("_BBodhiBurns","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_BBodhiBurns","GLOBAL",5)
	Range([PC],3)
THEN
	RESPONSE #100
	TransformItem("_bbohar2","_bbohair")
	SetDialog("_bBod25p")
	SetGlobal("_BBodhiBurns","GLOBAL",7)
END

IF
	Global("_BBodhiBurns","GLOBAL",7)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Script for First Hexxat Cloak Talk, next script in Hexxat
IF
	InParty("_BBodhi")
	InParty("HEXXAT")
	HasItemEquiped("ohhcloak","_bBodhi")
	!HasItem("ohhcloak","Hexxat")
	Global("_bCloakTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bCloakTalk","GLOBAL",1)
END

//Script for 2nd Hexxat Cloak Talk
IF
	InParty("_BBodhi")
	InParty("HEXXAT")
	HasItemEquiped("ohhcloak","_bBodhi")
	HasItemEquiped("ohhcloak","Hexxat")
	OR(2)
	Global("_bCloakTalk","GLOBAL",0)
	Global("_bCloakTalk","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bCloakTalk","GLOBAL",3)
END

IF
	Global("_bCloakTalk","GLOBAL",3)
	InParty("_BBodhi")
	!StateCheck("_BBodhi",CD_STATE_NOTVALID)
	InParty("HEXXAT")
	!StateCheck("HEXXAT",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Script when you first summon Bodhi.
IF
	IfValidForPartyDialog(Myself)
	CombatCounter(0)
	Global("_bBodhiHasSoul","GLOBAL",0)
	Global("_bBodhiFirstTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bBodhiFirstTalk","GLOBAL",1)
END

IF
	Global("_bBodhiFirstTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

	

