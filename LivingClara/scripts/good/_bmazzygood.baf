IF	//Trigger for Mazzy joining Keldorn if Keldorn attacks CHARNAME.
	!InParty("Keldorn")
	Allegiance("Keldorn",ENEMY)
	Detect("Keldorn")
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Minsc if Minsc attacks CHARNAME.
	!InParty("Minsc")
	Allegiance("Minsc",ENEMY)
	Detect("Minsc")
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Aerie if Aerie attacks CHARNAME.
	!InParty("Aerie")
	Allegiance("Aerie",ENEMY)
	Detect("Aerie")
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Jaheira if Jaheira attacks CHARNAME.
	!InParty("Jaheira")
	Allegiance("Jaheira",ENEMY)
	Global("_bMazzyHostile","GLOBAL",0)
	Detect("Jaheira")
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Imoen if Imoen attacks CHARNAME.
	!InParty("Imoen")
	Allegiance("Imoen",ENEMY)
	Detect("Imoen")
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Imoen2 if Imoen2 attacks CHARNAME.
	!InParty("Imoen2")
	Allegiance("Imoen2",ENEMY)
	Detect("Imoen2")
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Rasaad if Rasaad attacks CHARNAME.
	!InParty("RASAAD")
	Allegiance("RASAAD",ENEMY)
	Detect("RASAAD")
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Valygar if Valygar attacks CHARNAME.
	!InParty("Valygar")
	Allegiance("Valygar",ENEMY)
	Global("_bMazzyHostile","GLOBAL",0)
	Detect("Valygar")
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF	//Trigger for Mazzy joining Nalia if Nalia attacks CHARNAME.
	!InParty("Nalia")
	Allegiance("Nalia",ENEMY)
	Detect("Nalia")
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",1)
END


IF //Interjection trigger for turning Mazzy hostile after an Interject_Copy_Trans. (_bmazzyhostilesoa.d)
	Global("_bMazzyHostile","GLOBAL",1)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


IF //Bug fix for triggering Mazzy hostile if your reputation gets too low. (_bmazzyhostilesoa.d)
	ReputationLT(Player1,6)
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy") 
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",3)
END

IF	
	Global("_bMazzyHostile","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF	//To stop Mazzy from attacking Dorn later.
	Global("OHD_PLOT","GLOBAL",1)
	Global ("_bmazzydoesntstopdorn","GLOBAL",0)
	!AreaCheck("AR0900")
THEN
	RESPONSE #100
	SetGlobal("_bmazzydoesntstopdorn","GLOBAL",1)
END

IF	//Mazzy interjection if you agree to help Dorn kill the priest. Dialogue in _bmazzydorn.d
	IfValidForPartyDialog("Mazzy")
	OR(2)
	!IfValidForPartyDialogue("ANOMEN")
	Global("AnomenIsNotKnight","GLOBAL",1)
	!IfValidForPartyDialogue("KELDORN")
	!Global("_bmazzydoesntstopdorn","GLOBAL",1)
	Global("_bmazzystopsdorn","GLOBAL",0)
	Global("OHD_PLOT","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bmazzystopsdorn","GLOBAL",1)
END

IF
	Global("_bmazzystopsdorn","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF	//Trigger to reset Edwin quest if finished.
	!Global("EdwinJob","GLOBAL",1)
	Global("KickedOut","LOCALS",2)
THEN
	RESPONSE #100	
	SetGlobal("KickedOut","LOCALS",0)
END

IF //Mazzy's Hexxat Attack Script
	Global("_bMazHexDie","GLOBAL",1)
	IfValidForPartyDialog("Mazzy") 
THEN
	RESPONSE #100
	SetGlobal("NPCFight","LOCALS",1)
	SetGlobal("KickedOut","LOCALS",2)
	ChangeAIScript("mazzyx",SPECIFICS)
	ChangeAIScript("useitem",GENERAL)
	SetDialog("")
	ActionOverride("Mazzy",Attack("Hexxat"))
	ChangeAIScript("",DEFAULT)
	LeaveParty() 
	Enemy()
END

//Triggers to make Mazzy talk to you after Hexxat Hunt
IF
	Global("_bHexxatHunt","GLOBAL",1)
	OR(2)
	Global("_bEdwinVaraCloak","GLOBAL",1)
	Global("_bHouseParty","GLOBAL",1)
THEN
	RESPONSE #100
	Wait(3)
	StartDialogNoSet(Player1)
END

//Trigger to make Mazzy talk after you smuggle slaves in thieves guild
IF 
	Global("_bThiefSlaves","GLOBAL",1)
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	Wait(1)
	StartDialogNoSet(Player1)
END


//Triggers for Hostile Mazzy temples
//Lathander
IF
	Global("_bMazzyHostile","GLOBAL",0)
	GlobalGT("TempleShout0902","GLOBAL",0) 
	AreaCheck("AR0902")
	!Dead("dawnmas")
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",3)
END

//Helm
IF
	Global("_bMazzyHostile","GLOBAL",0)
	GlobalGT("TempleShout0901","GLOBAL",0) 
	AreaCheck("AR0901")
	!Dead("helmpr")
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",3)
END

//Order of the Radiant Heart
IF
	Global("_bMazzyHostile","GLOBAL",0)
	GlobalGT("TempleShout0903","GLOBAL",0) 
	OR(2)
		AreaCheck("AR0903")
		AreaCheck("AR0900")
	!Dead("hprelate") //Prelate Wessalen
	IfValidForPartyDialog("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",3)
END

//Trigger for Mazzy going hostile in Helmite Camp
IF
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy") 
	Allegiance("OHDHOKK",ENEMY)
THEN
	RESPONSE #100
	SetGlobal("_bMazzyHostile","GLOBAL",3)
END

//Resurrection Gorge Triggers
IF
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
	Global("OHD_ritual","OH5120",6)
	Global("OHD_ritual_target","OH5120",2)  //Anomen
	!Global("AnomenIsNotKnight","GLOBAL",1) //Anomen  (only if not not a knight)
	Global("_bMazzyRitualMad","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMazzyRitualMad","GLOBAL",1)
END

IF
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
	Global("OHD_ritual","OH5120",6)
	Global("OHD_ritual_target","OH5120",14)  //Viconia
	GlobalGT("_bMazzyVicFriends","GLOBAL",0) //Viconia â€“ if friends
	Global("_bMazzyRitualMad","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMazzyRitualMad","GLOBAL",1)
END

IF
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
	Global("OHD_ritual","OH5120",6)
	Global("OHD_ritual_target","OH5120",5) //HaerDalis
	!Global("_bHDDORNROMANCE","GLOBAL",2)  //HD not romancing Dorn
	Global("_bMazzyRitualMad","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMazzyRitualMad","GLOBAL",1)
END

IF
	Global("_bMazzyHostile","GLOBAL",0)
	IfValidForPartyDialog("Mazzy")
	Global("OHD_ritual","OH5120",6)
	OR(12)
	Global("OHD_ritual_target","OH5120",19) //Vernus
	Global("OHD_ritual_target","OH5120",1) //Aerie
	Global("OHD_ritual_target","OH5120",3) //Cernd
	Global("OHD_ritual_target","OH5120",6) //Imoen
	Global("OHD_ritual_target","OH5120",7) //Jaheira
	Global("OHD_ritual_target","OH5120",8) //Jan
	Global("OHD_ritual_target","OH5120",11) //Minsc
	Global("OHD_ritual_target","OH5120",12) //Nalia
	Global("OHD_ritual_target","OH5120",13) //Valygar
	Global("OHD_ritual_target","OH5120",15) //Neera
	Global("OHD_ritual_target","OH5120",16) //Yoshimo
	Global("OHD_ritual_target","OH5120",18) //Rasaad
	Global("_bMazzyRitualMad","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMazzyRitualMad","GLOBAL",1)
END

IF
	Global("_bMazzyRitualMad","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
	