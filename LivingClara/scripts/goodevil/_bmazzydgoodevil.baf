//Mazzy uses same d script for both SOA & TOB, anything that triggers in SOA or in both is in this file. TOB only is in that file.
IF //Mazzy-Jan Rest Scene. SOA & TOB Rest of script in mazzygoodeviltob.baf or mazzygoodevil.baf
	Global("_bmazzyjan","GLOBAL",1)
	GlobalTimerExpired("_bmjtimer1","GLOBAL")
	InParty("Jan") 
	!StateCheck("Jan",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	OR(3)
		AreaType(DUNGEON)
		AreaType(OUTDOOR)
		AreaCheck("AR4500")
	Global("_brestcheck","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bmazzyjan","GLOBAL",2)
	SetGlobal("_brestcheck","GLOBAL",2)
END

//Shade Lord Talk Timer - SOA only
IF
	Global("_bmazzyShadeLT","GLOBAL",0)
	Global("_BMazzyFriendship","GLOBAL",1)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	AreaCheck("AR2100")
	Global("_brestcheck","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bmazzyShadeLT","GLOBAL",1)
	SetGlobal("_brestcheck","GLOBAL",2)
END

//6th Viconia conversation, SOA & TOB Rest of script in mazzygoodeviltob.baf or mazzygoodevil.baf
IF
	Global("_bMazVicConv","GLOBAL",14)
	GlobalTimerExpired("_bmazvictimer6","GLOBAL")
	InParty("Viconia") 
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("Mazzy")
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	Global("_brestcheck","GLOBAL",0)
	OR(3)
		AreaType(DUNGEON)
		AreaType(OUTDOOR)
		AreaCheck("AR4500")
	Global("_bMazzyVicFriends","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",15)
	SetGlobal("_brestcheck","GLOBAL",2)
END

//12th Talk Timer - no romances, not in Underdark - SOA Version
IF
	Global("_bMazzyVicFriends","GLOBAL",2)
	InParty("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("Mazzy")
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	Global("_bMVBathTalk","GLOBAL",0)
	Global("_bMazzyRomance","GLOBAL",0)
	!Global("SleptViconia","GLOBAL",1)
	Global("_brestcheck","GLOBAL",0)
	OR(17)
		AreaCheck("AR0021")        // City Gates - Crooked Crane 1st floor
		AreaCheck("AR0022")        // City Gates - Crooked Crane 2nd floor
		AreaCheck("AR0313")        // Docks - Sea's Bounty 1st floor
	    AreaCheck("AR0314")        // Docks - Sea's Bounty 2nd floor	
		AreaCheck("AR0406")        // Slums - Copper Coronet
		AreaCheck("AR0509")        // Bridge - Five Flagons 1st floor
		AreaCheck("AR0511")        // Bridge - Five Flagons 2nd Floor
		AreaCheck("AR0513")       // Bridge - Calbor's Inn 1st floor
		AreaCheck("AR0514")        //Bridge - Inn 2nd floor
		AreaCheck("AR0515")        //Bridge - Inn 3rd floor
		AreaCheck("AR0522")        // Bridge - Five Flagons 1st floor (stronghold)
		AreaCheck("AR0704")        // Waukeen's Promenade - Mithrest Inn
		AreaCheck("AR0709")        // Waukeen's Promenade - Den of the Seven Vales, 1st Floor
		AreaCheck("AR0712")        // Waukeen's Promenade - Den of the Seven Vales, 2nd Floor
		AreaCheck("AR1105")        // Umar Hills - Imnesvale Inn
		AreaCheck("AR1602")        // Brynnlaw - Brynnlaw Inn
		AreaCheck("AR2010")        // Trademeet - Vytori's Pub
THEN
	RESPONSE #100
	SetGlobal("_bMVBathTalk","GLOBAL",1)
	SetGlobal("_brestcheck","GLOBAL",2)
END

//M-V Mazzy CN Romance Talk
IF
	Global("_bMazzyVicFriends","GLOBAL",2)
	InParty("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("Mazzy")
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	Global("_bMVCNLoveTalk","GLOBAL",0)
	Global("_bMazzyRomance","GLOBAL",1)
	Global("_bMazNotVic","GLOBAL",0)
	GlobalLT("_bMazzyTQuest","GLOBAL",4)
	GlobalLT("_bMazzyVQuest","GLOBAL",4)
	Global("_brestcheck","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMVCNLoveTalk","GLOBAL",1)
	SetGlobal("_brestcheck","GLOBAL",2)
END
