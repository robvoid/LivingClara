IF	//Trigger for Keldorn leaving the party forever.
	Global("_bKeldLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END

//ClaraSex scene trigger - romance
IF
	Global("_bSleepsWithClara","GLOBAL",4) 
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	Global("_bSWCKe","GLOBAL",0)
	InMyArea("_bClara")
	InParty("_bClara") 
	!StateCheck("_bClara",CD_STATE_NOTVALID) 
	OR(2) 
		Global("B!KeldornRA","GLOBAL",2) 
		Global("B!KeldornRA","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//ClaraSex scene trigger - no romance
IF
	Global("_bSleepsWithClara","GLOBAL",4) 
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	Global("_bSWCKe","GLOBAL",0)
	InParty("_bClara") 
	!StateCheck("_bClara",CD_STATE_NOTVALID)  
	!Global("B!KeldornRA","GLOBAL",2) 
	!Global("B!KeldornRA","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Triggers for 1st new Keldorn-Mazzy Dialogue (in _bmazzykeldornsoa.d)
IF
	InParty("Keldorn") 
	InParty("Mazzy") 
	Global("_bmazkel","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bmazkel","GLOBAL",1)
	SetGlobalTimer("_bmazzykeldtimer1","GLOBAL",TEN_DAYS)
END

IF
	Global("_bmazkel","GLOBAL",1)
	GlobalTimerExpired("_bmazzykeldtimer1","GLOBAL")
	InParty("Keldorn") 
	InParty("Mazzy") 
THEN
	RESPONSE #100
	SetGlobal("_bmazkel","GLOBAL",2)
END
	
IF
	Global("_bmazkel","GLOBAL",2)
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Triggers for 2nd Mazzy dialogue
IF
	InParty("Keldorn") 
	InParty("Mazzy") 
	Global("_bmazkel","GLOBAL",3)
THEN
	RESPONSE #100
	SetGlobal("_bmazkel","GLOBAL",4)
	SetGlobalTimer("_bmazzykeldtimer2","GLOBAL",TWO_DAYS)
END

IF
	Global("_bmazkel","GLOBAL",4)
	GlobalTimerExpired("_bmazzykeldtimer2","GLOBAL")
	InParty("Keldorn") 
	InParty("Mazzy") 
THEN
	RESPONSE #100
	SetGlobal("_bmazkel","GLOBAL",5)
END
	
IF
	Global("_bmazkel","GLOBAL",5)
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Trigger for Keldorn to go talk to Prelate
IF
	Global("_bMazzyJoinOrder","GLOBAL",3) 
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
	MoveToObject("hprelate")
	Wait(2)
	ActionOverride("hprelate",StartDialogueNoSet(Player1))
END

//Trigger for 1st talk about Viconia
IF
	InParty("Keldorn") 
	InParty("Mazzy") 
	InParty("Viconia")
	Global("_bmazkelvic","GLOBAL",0)
	OR(2)
	Global("MazzyVicFriends","GLOBAL",1)
	Global("MazzyVicFriends","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bmazkelvic","GLOBAL",1)
END

IF
	Global("_bmazkelvic","GLOBAL",1)
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty("Viconia")
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Second Talk about Viconia
IF
	InParty("Keldorn") 
	InParty("Viconia")
	Global("_bmazkelvic","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bmazkelvic","GLOBAL",3)
	SetGlobalTimer("_bMKVTimer1","GLOBAL",THREE_DAYS)
END

IF
	InParty("Keldorn") 
	InParty("Viconia")
	Global("_bmazkelvic","GLOBAL",3)
	GlobalTimerExpired("_bMKVTimer1","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bmazkelvic","GLOBAL",4)
END

IF
	Global("_bmazkelvic","GLOBAL",4)
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Recoding of Keldorn's Script to attack Dorn
IF
	InParty(Myself)
	IsValidForPartyDialog(Myself)
	Global("OHD_PLOT","GLOBAL",1)
	InParty("dorn")  // Dorn
	IsValidForPartyDialog("dorn")  // Dorn
	Global("_bkeldornattacksdornattemple","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("_bkeldornattacksdornattemple","GLOBAL",1)
		Continue()
END

IF
	Global("_bkeldornattacksdornattemple","GLOBAL",1)
THEN
	RESPONSE #100
		Dialogue(Player1)
END

//Windspear Hills Quest
IF
	Global("MostNobleOrder","GLOBAL",0)
	Global("_bKeldornFearsOrder","GLOBAL",0)
	IfValidForPartyDialog("Keldorn")
	OR(2)
		Global("FollowGarren","GLOBAL",1)
		Global("FollowedGarren","GLOBAL",1)
	OR(8)
		AreaCheck("AR0020") // City Gates
		AreaCheck("AR0300") //  The docks
		AreaCheck("AR0400") // The slums
		AreaCheck("AR0500 ") // Bridge district
		AreaCheck("AR0700") // Waukeen's Promenade
		AreaCheck("AR0800") // Graveyard
		AreaCheck("AR0900") // Temple District
		AreaCheck("AR1000") // Government District
THEN
	RESPONSE #100
	SetGlobal("_bKeldornFearsOrder","GLOBAL",1)
END

IF
	Global("_bKeldornFearsOrder","GLOBAL",1)
THEN RESPONSE #100
	StartDialogNoSet(Player1)
END
