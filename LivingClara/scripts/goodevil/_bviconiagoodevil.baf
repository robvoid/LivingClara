IF	//Viconia leaves forever
	Global("_bVicLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()	
	LeaveParty() 
	EscapeArea()
END

IF	//Trigger for Mazzy/Viconia conversation if former rescued latter
	Global("_bMazzySavesViconia","GLOBAL",1)
	InParty("Viconia")
	InParty("Mazzy") 
THEN
	RESPONSE #100
	SetGlobal("_bMazzySavesViconia","GLOBAL",2)
	SetGlobalTimer("_bmazvictimer1","GLOBAL",TWO_HOURS)
END

IF
	Global("_bMazzySavesViconia","GLOBAL",2)
	GlobalTimerExpired("_bmazvictimer1","GLOBAL")
	IfValidForPartyDialogue("Viconia") 
	IfValidForPartyDialogue("Mazzy") 
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazzySavesViconia","GLOBAL",3)
END

IF
	Global("_bMazzySavesViconia","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//First conversation if both join party w/o the rescued
IF
	Global("_bMazzySavesViconia","GLOBAL",0)
	Global("_bMazVicConv","GLOBAL",0)
	Global("_bMazVicExtra","GLOBAL",0)
	IfValidForPartyDialogue("Viconia") 
	IfValidForPartyDialogue("Mazzy") 
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicExtra","GLOBAL",1)
END

IF
	Global("_bMazVicConv","GLOBAL",0)
	Global("_bMazVicExtra","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END



//2nd/4th conversation - this is where the two different chains come together
IF
	Global("_bMazVicConv","GLOBAL",7)
	InParty("Viconia")
	InParty("Mazzy") 
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",8)
	SetGlobalTimer("_bmazvictimer4","GLOBAL",TWO_DAYS)
END

IF
	Global("_bMazVicConv","GLOBAL",8)
	GlobalTimerExpired("_bmazvictimer4","GLOBAL")
	IfValidForPartyDialogue("Viconia") 
	IfValidForPartyDialogue("Mazzy") 
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",9)
END

IF
	Global("_bMazVicConv","GLOBAL",9)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//8th Conversation Timer - set duel
IF
	Global("_bMazVicConv","GLOBAL",19)
	InParty("Viconia")
	InParty("Mazzy")
	Global("_bMazzyVicFriends","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",20)
	SetGlobalTimer("_bmazvictimer8","GLOBAL",ONE_DAY)
END

IF
	Global("_bMazVicConv","GLOBAL",20)
	GlobalTimerExpired("_bmazvictimer8","GLOBAL")
	IfValidForPartyDialogue("Viconia") 
	IfValidForPartyDialogue("Mazzy") 
	Global("_bMazzyVicFriends","GLOBAL",1)
	HaveKnownSpell(CLERIC_RAISE_DEAD)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",21)
END

IF
	Global("_bMazVicConv","GLOBAL",21)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//11th talk timer - next trigger in d script
IF
	Global("_bMazVicConv","GLOBAL",26)
	InParty("Viconia")
	InParty("Mazzy")
	Global("_bMazzyVicFriends","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",27)
	SetGlobalTimer("_bmazvictimer9","GLOBAL",THREE_DAYS)
END

IF
	Global("_bMazVicConv","GLOBAL",28)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Bath Talk 2- no romance (1st trigger in d script)
IF
	Global("_bMVBathTalk","GLOBAL",5)
THEN
	RESPONSE #100
	UndoExplore()
	StartDialogNoSet(Player1)
END

//13th Talk
IF
	Global("_bMazVicConv","GLOBAL",32)
	InParty("Viconia")
	InParty("Mazzy")
	Global("_bMazzyVicFriends","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",33)
	SetGlobalTimer("_bmazvictimer11","GLOBAL",THREE_DAYS)
END

IF
	Global("_bMazVicConv","GLOBAL",33)
	GlobalTimerExpired("_bmazvictimer11","GLOBAL")
	IfValidForPartyDialogue("Viconia") 
	IfValidForPartyDialogue("Mazzy")
	Global("_bMazzyVicFriends","GLOBAL",2)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicConv","GLOBAL",34)
END

IF
	Global("_bMazVicConv","GLOBAL",34)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//ClaraSex scene trigger
IF
	InParty("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty("_bClara")
	!StateCheck("_bClara",CD_STATE_NOTVALID)
	Global("ViconiaRomanceActive","GLOBAL",2) 
	Global("_bSWCV","GLOBAL",0)
	Global("_bSleepsWithClara","GLOBAL",4)
THEN
	RESPONSE #100
	ActionOverride(Player1,SetDialog("Player1"))
	StartDialogNoSet(Player1)
END

//Trigger for Bad Sex Talk - 1st in d script
IF
	Global("_bMVSexTalk","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(40)
	StartDialogNoSet(Player1)
END

//2nd Sex Talk Timer - if friends, Viconia Romance, & asked about Mazzy romance but didn't start it
IF
	InParty("Viconia")
	InParty("Mazzy")
	Global("_bMazzyRomance","GLOBAL",0)
	Global("ViconiaRomanceActive","GLOBAL",2)
	Global("_bMazzyVicFriends","GLOBAL",2)
	Global("_bMVSexTalk2","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMVSexTalk2","GLOBAL",1)
	SetGlobalTimer("_bmvsextimer","GLOBAL",TWO_DAYS)
END

IF
	Global("_bMVSexTalk2","GLOBAL",1)
	GlobalTimerExpired("_bmvsextimer","GLOBAL")
	IfValidForPartyDialogue("Viconia") 
	IfValidForPartyDialogue("Mazzy")
	Global("_bMazzyVicFriends","GLOBAL",2)
	Global("_bMazzyRomance","GLOBAL",0)
	Global("_BMazzyCourt","GLOBAL",2)
	Global("ViconiaRomanceActive","GLOBAL",2)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMVSexTalk2","GLOBAL",2)
END

IF
	Global("_bMVSexTalk2","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Mazzy-Viconia 3SomeTalk
IF
	Global("_bMazzyVicFriends","GLOBAL",2)
	IfValidForPartyDialogue("Viconia")
	IfValidForPartyDialogue("Mazzy")
	Global("_bMazzyRomance","GLOBAL",2)
	GlobalTimerExpired("_bMazSexTimer","GLOBAL")
	Global("_bMVSexTalk3","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMVSexTalk3","GLOBAL",1)
END

IF
	Global("_bMVSexTalk3","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END

//Trigger for Vic Shaping Fade & Line
IF
	IfValidForPartyDialogue("Viconia")
	IfValidForPartyDialogue("Mazzy")
	Global("_bMVSexTalk3","GLOBAL",4)
	Global("bVicShaped","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bMVSexTalk3","GLOBAL",5)
END

IF
	Global("_bMVSexTalk3","GLOBAL",5)
THEN
	RESPONSE #100
	FadeToColor([20.0],0)
	Wait(2)
	SetGlobal("_bMVSexTalk3","GLOBAL",6)
	AdvanceTime(ONE_HOUR)
END

IF
	Global("_bMVSexTalk3","GLOBAL",6)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Post 3Some Talk Timer
IF
	InParty("Viconia")
	Global("_bVicMaz3Some","GLOBAL",1)
	Global("_bPostMV3Talk","GLOBAL",0)
	Global("_bVicJoin","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bPostMV3Talk","GLOBAL",1)
	SetGlobalTimer("_bmv3timer","GLOBAL",ONE_HOUR)
END

IF
	IfValidForPartyDialogue("Viconia")
	Global("_bPostMV3Talk","GLOBAL",1)
	GlobalTimerExpired("_bmv3timer","GLOBAL")
	Global("_bVicJoin","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bPostMV3Talk","GLOBAL",2)
END

IF
	Global("_bPostMV3Talk","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Breakup Talk if CN Chose Mazzy - plays immediately, both versions
IF
	IfValidForPartyDialogue("Viconia")
	IfValidForPartyDialogue("Mazzy")
	Gender(Player1,MALE)
	Global("_bMazNotVic","GLOBAL",1)
	Global("_bMVBreakup","GLOBAL",0)
	!Global("_bMazzyVicFriends","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMVBreakup","GLOBAL",1)
END

IF
	Global("_bMVBreakup","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END

//Viconia-Aerie Talks. Next trigger in Aerie
IF
	IfValidForPartyDialogue("Aerie") 
	IfValidForPartyDialogue("Viconia")
	Global("_bAerieVicArmTalk","GLOBAL",0)
	HaveKnownSpell(CLERIC_REGENERATE)
	HPPercentLT("Viconia",50)
THEN
	RESPONSE #100
	SetGlobal("_bAerieVicArmTalk","GLOBAL",1)
END

//Viconia-Aerie Talks. Second Half of 1st
IF
	IfValidForPartyDialogue("Aerie") 
	IfValidForPartyDialogue("Viconia")
	Global("_bAerieVicArmTalk","GLOBAL",2)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bAerieVicArmTalk","GLOBAL",3)
END

IF
	Global("_bAerieVicArmTalk","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//3rd Aerie Talk Timer 
IF
	InParty("Aerie") 
	InParty("Viconia")
	Global("_bAerieVicHelp","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bAerieVicHelp","GLOBAL",2)
	SetGlobalTimer("_bVicAerieArm2","GLOBAL",ONE_DAY)
END

IF
	IfValidForPartyDialogue("Aerie") 
	IfValidForPartyDialogue("Viconia")
	Global("_bAerieVicHelp","GLOBAL",2)
	GlobalTimerExpired("_bVicAerieArm2","GLOBAL")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bAerieVicHelp","GLOBAL",3)
END

IF
	Global("_bAerieVicHelp","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//3rd Aerie Talk - 2nd Half
IF
	IfValidForPartyDialogue("Aerie") 
	IfValidForPartyDialogue("Viconia")
	Global("_bAerieVicHelp","GLOBAL",4)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bAerieVicHelp","GLOBAL",5)
END

IF
	Global("_bAerieVicHelp","GLOBAL",5)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//5th Aerie Talk - 2nd Half if removes wings
IF
	Global("_bAerieRefuseVic","GLOBAL",7)
	IfValidForPartyDialogue("Aerie") 
	IfValidForPartyDialogue("Viconia")
	CombatCounter(0)
THEN
	RESPONSE #100
	DestroyItem("_bWINGV")
	GiveItemCreate("_bWINGV","Viconia",0,0,0)
	SetGlobal("_bAerieRefuseVic","GLOBAL",8)
END

IF
	Global("_bAerieRefuseVic","GLOBAL",8)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Code to Fix Mazzy Friendship for people who played earlier mod versions
IF
	GlobalGT("_bMazVicConv","GLOBAL",9)
	GlobalLT("_bMazVicConv","GLOBAL",29)
	Global("_bMazVicFriendsFix","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMazVicFriendsFix","GLOBAL",1)
	SetGlobal("_bMazzyVicFriends","GLOBAL",1)
END

//Banter thing
IF
	OR(3)
		Global("_bMazzyVicFriends","GLOBAL",2)
		Global("_bMazzyVicFriends","GLOBAL",1)
		!Global("_bMazzySavesViconia","GLOBAL",0)
	!G("FWBanterMazzyViconia",2)
	
THEN
	RESPONSE #100
	SG("FWBanterMazzyViconia",2)
END
	
//Mazzy's Mom Talk
IF
	IfValidForPartyDialogue("Viconia")
	Global("_bVaraHouseInvite","GLOBAL",0)
	Global("_bMazzyVicFriends","GLOBAL",2)
	Detect("vara")
THEN
	RESPONSE #100
	SetGlobal("_bVaraHouseInvite","GLOBAL",1)
END

IF
	Global("_bVaraHouseInvite","GLOBAL",1)
THEN
	RESPONSE #100
	ActionOverride("vara",StartDialogNoSet(Player1))
END

//Aerie Wing Reponse
IF
	Global("_bAerieWingResponse","GLOBAL",1)
	IfValidForPartyDialogue("Viconia")
	Global("_bViconiaWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END
