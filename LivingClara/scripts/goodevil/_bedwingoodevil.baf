IF	//Trigger for first conversation after 1st Edwin-Mazzy in-game banter
	Global("BEdwin3","LOCALS",1)
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	Global("_bedwinmazzy","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bedwinmazzy","GLOBAL",1)
	SetGlobalTimer("_bedwinmazzy1","GLOBAL",ONE_DAY)
END

IF	Global("_bedwinmazzy","GLOBAL",1)
	GlobalTimerExpired("_bedwinmazzy1","GLOBAL")
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
	SetGlobal("_bedwinmazzy","GLOBAL",2)
END

IF
	Global("_bedwinmazzy","GLOBAL",2)
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//ClaraSex scene trigger
IF
	Global("_bSleepsWithClara","GLOBAL",4) 
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	Global("_bSWCE","GLOBAL",0)
	Global("EdwinNightTogether","GLOBAL",1) 
	!Global("EdwinRomanceActive","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Mazzy should be in your party during Elvira scene to say this, but could only code close enough.
//Also requires earlier Edwin conversations so that the bodyswap part makes sense
IF
	Global("BancoDead","GLOBAL",1)
	Global("DekkieBye","GLOBAL",1)
	Global("EdwinLoveTalk","LOCALS",43)
	InParty("Edwin") 
	InParty("Mazzy") 
	Global("_bMotherCheck","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMotherCheck","GLOBAL",1)
END
	
IF	
	GlobalGT("BeginEdElDek","LOCALS",0)
	!Global("EdwinRomanceActive","GLOBAL",3)
	Global("_bedwinmazzy","GLOBAL",12)
	Global("_bMotherCheck","GLOBAL",1)
	InParty("Edwin") 
	InParty("Mazzy") 
THEN
	RESPONSE #100	
	SetGlobal("_bedwinmazzy","GLOBAL",13)
	SetGlobalTimer("_bedwinmazzy5","GLOBAL",THREE_HOURS)
END

//To make him selectable again after turns into a woman if it glitches
IF
	Global("AfterChange","LOCALS",2)
	Global("_bEdwinFix","GLOBAL",0)
THEN
	RESPONSE #100
	MakeUnselectable(0)
	SetGlobal("_bEdwinFix","GLOBAL",1)
END

//Trigger for Breakup Talk. 3 hours to give time for research
IF
	Global("EdwinRomanceActive","GLOBAL",3) 
	Global("_bMazNotEdwin","GLOBAL",1)
	InParty("Edwin") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobalTimer("_bEdwinBreakUp","GLOBAL",THREE_HOURS)
	SetGlobal("_bMazNotEdwin","GLOBAL",2)
END

IF
	GlobalTimerExpired("_bEdwinBreakUp","GLOBAL") 
	Global("_bMazNotEdwin","GLOBAL",2)
	InParty("Edwin") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bMazNotEdwin","GLOBAL",3)
END

IF
	Global("_bMazNotEdwin","GLOBAL",3)
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END


//Trigger for Edwin/Aerie Wing Cutscene
IF
	InParty("Edwin")
	InParty("Aerie")
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	Global("_bEdwinAerieWings","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bEdwinAerieWings","GLOBAL",1)
	SetGlobalTimer("_bAWingTimer","GLOBAL",TEN_DAYS)
END

IF 
	InParty("Edwin")
	InParty("Aerie")
	Global("_bEdwinAerieWings","GLOBAL",1)
	GlobalTimerExpired("_bAWingTimer","GLOBAL")
	GlobalGT("TalkedDegardan","GLOBAL",2) //after Edwin changes back from Edwina
THEN
	RESPONSE #100
	SetGlobalTimer("_bELBTimer","GLOBAL",TWO_DAYS)
	SetGlobal("_bEdwinAerieWings","GLOBAL",100)	
END


IF 
	InParty("Edwin")
	InParty("Aerie")
	Global("_bEdwinAerieWings","GLOBAL",100)
	GlobalTimerExpired("_bELBTimer","GLOBAL")
THEN
	RESPONSE #100	
	SetGlobal("_bEdwinAerieWings","GLOBAL",2)
END

IF
	InParty("Edwin")
	InParty("Aerie")
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	Global("_bEdwinAerieWings","GLOBAL",2)
	AreaType(OUTDOOR)
	CombatCounter(0)
THEN
	RESPONSE #100
	FadeToColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",3)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",3)
THEN
	RESPONSE #100
	Polymorph(MAGE_FEMALE_ELF)
	ApplySpellRES("_bELFP","Edwin")
	GiveItemCreate("_BWING","Edwin",0,0,0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",4)
	XEquipItem("_BWING","Edwin",SLOT_HELMET,EQUIP)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",4)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",6)
THEN
	RESPONSE #100
	CreateVisualEffectObject("SPPOLYMP","Aerie")
	Wait(2)
	FadeToColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",7)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",7)
THEN
	RESPONSE #100
	ApplySpellRES("_BEDWP","Edwin")
	Polymorph(MAGE_MALE_HUMAN)
	DestroyItem("_BWING")
	ApplySpellRES("_bAERP","Aerie")
	ActionOverride("Aerie",DestroyItem("_BWING"))
	Wait(1)
	SetGlobal("_bEdwinAerieWings","GLOBAL",8)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",8)
THEN
	RESPONSE #100
	FadeFromColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",9)
END

//Aerie Wing Reponse
IF
	Global("_bAerieWingResponse","GLOBAL",1)
	InParty("Edwin")
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	GlobalGT("_bEdwinAerieWings","GLOBAL",4)
	Global("_bEdwinWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END
