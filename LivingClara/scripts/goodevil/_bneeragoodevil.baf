IF	//Trigger for Neera leaving the party forever.
	Global("_bNeeraLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2) 
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END

//ClaraSex scene trigger
IF
	Global("_bSleepsWithClara","GLOBAL",4) 
	IfValidForPartyDialog("NEERA") 
	Global("_bSWCNe","GLOBAL",0)
	OR(2) 
		Global("NeeraRomanceActive","GLOBAL",1) 
		Global("NeeraRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END



//Code for Neera-Minsc Romance. All the talks with the same timer.
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(7)
	Global("_bNeeraMinscLove","GLOBAL",0)
	Global("_bNeeraMinscLove","GLOBAL",3)
	Global("_bNeeraMinscLove","GLOBAL",6)
	Global("_bNeeraMinscLove","GLOBAL",9)
	Global("_bNeeraMinscLove","GLOBAL",15)
	Global("_bNeeraMinscLove","GLOBAL",18)
	Global("_bNeeraMinscLove","GLOBAL",21)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bNMLoveTime","GLOBAL",FOUR_DAYS)
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	GlobalTimerExpired("_bNMLoveTime","GLOBAL")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(7)
	Global("_bNeeraMinscLove","GLOBAL",1)
	Global("_bNeeraMinscLove","GLOBAL",4)
	Global("_bNeeraMinscLove","GLOBAL",7)
	Global("_bNeeraMinscLove","GLOBAL",10)
	Global("_bNeeraMinscLove","GLOBAL",16)
	Global("_bNeeraMinscLove","GLOBAL",19)
	Global("_bNeeraMinscLove","GLOBAL",22)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	OR(7)
	Global("_bNeeraMinscLove","GLOBAL",2)
	Global("_bNeeraMinscLove","GLOBAL",5)
	Global("_bNeeraMinscLove","GLOBAL",8)
	Global("_bNeeraMinscLove","GLOBAL",11)
	Global("_bNeeraMinscLove","GLOBAL",17)
	Global("_bNeeraMinscLove","GLOBAL",20)
	Global("_bNeeraMinscLove","GLOBAL",23)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Separate City Talk - only requires first talk
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	GlobalGT("_bMinscNeera","GLOBAL",0)
	Global("_bMNCityTalk","GLOBAL",0)
	AreaType(CITY)
	AreaType(OUTDOOR)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bMNCityTalk","GLOBAL",1)
END

IF
	Global("_bMNCityTalk","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Separate Inn Talk - only requires first one
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	GlobalGT("_bMinscNeera","GLOBAL",0)
	Global("_bMNInnTalk","GLOBAL",0)
	OR(17)
		AreaCheck("AR0021")       // City Gates - Crooked Crane 1st floor
		AreaCheck("AR0022")       // City Gates - Crooked Crane 2nd floor
		AreaCheck("AR0313")       // Docks - Sea's Bounty 1st floor
	    AreaCheck("AR0314")       // Docks - Sea's Bounty 2nd floor	
		AreaCheck("AR0406")       // Slums - Copper Coronet
		AreaCheck("AR0509")       // Bridge - Five Flagons 1st floor
		AreaCheck("AR0511")       // Bridge - Five Flagons 2nd Floor
		AreaCheck("AR0513")       // Bridge - Calbor's Inn 1st floor
		AreaCheck("AR0514")       // Bridge - Inn 2nd floor
		AreaCheck("AR0515")       // Bridge - Inn 3rd floor
		AreaCheck("AR0522")       // Bridge - Five Flagons 1st floor (stronghold)
		AreaCheck("AR0704")       // Waukeen's Promenade - Mithrest Inn
		AreaCheck("AR0709")       // Waukeen's Promenade - Den of the Seven Vales, 1st Floor
		AreaCheck("AR0712")       // Waukeen's Promenade - Den of the Seven Vales, 2nd Floor
		AreaCheck("AR1105")       // Umar Hills - Imnesvale Inn
		AreaCheck("AR1602")       // Brynnlaw - Brynnlaw Inn
		AreaCheck("AR2010")       // Trademeet - Vytori's Pub
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bMNInnTalk","GLOBAL",1)
END

IF
	Global("_bMNInnTalk","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//1st Romance Talk - Requires other City Talk & 1st 8 Talks
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	Global("_bNeeraMinscLove","GLOBAL",24)
	Global("_bMNCityTalk","GLOBAL",2)
	OR(8)
	AreaCheck("AR0020") //City Gates
	AreaCheck("AR0300") //Docks
	AreaCheck("AR0400") //Slums
	AreaCheck("AR0500") //Bridge
	AreaCheck("AR0700") //Waukeen's Promenade
	AreaCheck("AR0800") //Graveyard
	AreaCheck("AR0900") //Temple District
	AreaCheck("AR1000") //Government District
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bMNCityTalk","GLOBAL",3)
END

IF
	Global("_bMNCityTalk","GLOBAL",3)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//2nd Talk Sequence, once actually romancing
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bMinscNeera","GLOBAL",2)
	OR(4)
	Global("_bNeeraMinscLove","GLOBAL",24)
	Global("_bNeeraMinscLove","GLOBAL",27)
	Global("_bNeeraMinscLove","GLOBAL",32)
	Global("_bNeeraMinscLove","GLOBAL",37)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bNMLoveTime","GLOBAL",FOUR_DAYS)
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bNMLoveTime","GLOBAL")
	OR(4)
	Global("_bNeeraMinscLove","GLOBAL",25)
	Global("_bNeeraMinscLove","GLOBAL",28)
	Global("_bNeeraMinscLove","GLOBAL",33)
	Global("_bNeeraMinscLove","GLOBAL",38)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	OR(4)
	Global("_bNeeraMinscLove","GLOBAL",26)
	Global("_bNeeraMinscLove","GLOBAL",29)
	Global("_bNeeraMinscLove","GLOBAL",34)
	Global("_bNeeraMinscLove","GLOBAL",39)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Combat Talk, separate trigger
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bMinscNeera","GLOBAL",2)
	ActuallyInCombat()
	Global("_bMNFightTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMNFightTalk","GLOBAL",1)
END

IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	Global("_bMNFightTalk","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Post-rest triggers
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(2)
	Global("_bNeeraMinscLove","GLOBAL",30) //Post sex
	Global("_bNeeraMinscLove","GLOBAL",35) //Post story promise
	Global("_bMinscNeera","GLOBAL",2)
	PartyRested()
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	OR(2)
	Global("_bNeeraMinscLove","GLOBAL",31)
	Global("_bNeeraMinscLove","GLOBAL",36)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Post Hidden Refuge Talk
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bRefugeTalk","GLOBAL",0)
	Global("OHN_REFUGE_BURNED","GLOBAL",1)
	Global("_bMinscNeera","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bRefugeTalk","GLOBAL",1)
	SetGlobalTimer("_bNMRefugeTime","GLOBAL",TWO_DAYS)
END

IF	
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bNMRefugeTime","GLOBAL")
	Global("_bRefugeTalk","GLOBAL",1)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bRefugeTalk","GLOBAL",2)
END

IF
	Global("_bRefugeTalk","GLOBAL",2)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Trigger for Boo Cutscene Talk (1st trigger in d script)
IF
	Global("_bBooChange","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Aerie-Wings Talk - next trigger in Aerie.baf
IF 
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Aerie")
	Global("_bNeeraASpellTalk","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bNeeraASpellTalk","GLOBAL",1)
END


IF //Turns off 1st Clara Banter if Neera knows spell
	HaveKnownSpell(WIZARD_INVISIBILITY_10_FOOT)
	Global("_BBCLARANEERA","GLOBAL",0)
THEN
	RESPONSE #100	
	SetGlobal("_BBCLARANEERA","GLOBAL",1)
END

//Breakup Talk
IF
	Global("NeeraRomanceActive","GLOBAL",3) 
	Global("_bMazNotNeera","GLOBAL",1)
	InParty("NEERA") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobalTimer("_bNeeraBreakUp","GLOBAL",THIRTY_MINUTES)
	SetGlobal("_bMazNotNeera","GLOBAL",2)
END

IF
	GlobalTimerExpired("_bNeeraBreakUp","GLOBAL") 
	Global("_bMazNotNeera","GLOBAL",2)
	IfValidForPartyDialogue("NEERA")
	IfValidForPartyDialogue("Mazzy")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazNotNeera","GLOBAL",3)
END

IF
	Global("_bMazNotNeera","GLOBAL",3)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END
