IF	//Trigger for Imoen leaving the party forever.
	Global("_bImoenLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()	
	LeaveParty() 
	EscapeArea()
END

//Post Massage Talk
IF
	Global("_bImoenMazzyDadrow21","GLOBAL",1)
	InParty("Mazzy")
	InParty(Myself)
THEN
	RESPONSE #100
	SetGlobal("_brestcheck","GLOBAL",2)
	SetGlobal("_bImoenMazzyDadrow21","GLOBAL",2)
	ApplySpell("Imoen2",CLERIC_CURE_SERIOUS_WOUNDS)
	ActionOverride("Mazzy",ApplySpell("Mazzy",CLERIC_CURE_SERIOUS_WOUNDS))
	RestParty()
END
	
IF
	Global("_bImoenMazzyDadrow21","GLOBAL",2)
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END	

//Mazzy Talk Timer
IF
	InParty("Imoen2") 
	InParty("Mazzy") 
	Global("Chapter","GLOBAL",%bg2_chapter_4%)
	Global("_bIMLoveTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bIMLoveTalk","GLOBAL",1)
	SetGlobalTimer("_bImMaz1","GLOBAL",TWO_HOURS)
END

IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
	Global("_bIMLoveTalk","GLOBAL",1)
	GlobalTimerExpired("_bImMaz1","GLOBAL")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bIMLoveTalk","GLOBAL",2)
END

IF
	Global("_bIMLoveTalk","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
	
//Viconia Talk - doesn't require friends w/ Mazzy anymore
IF
	InParty("Imoen2") 
	InParty("Viconia")
	Global("_bIMLoveTalk","GLOBAL",3)
	Global("_bImVicTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bImVicTalk","GLOBAL",1)
	SetGlobalTimer("_bImVic1","GLOBAL",ONE_DAY)
END

IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Viconia")
	Global("_bImVicTalk","GLOBAL",1)
	GlobalTimerExpired("_bImVic1","GLOBAL")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bImVicTalk","GLOBAL",2)
END

IF
	Global("_bImVicTalk","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//2nd Talk Trigger - set in d script
IF
	Global("_bIMLoveTalk","GLOBAL",4)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Bath Talk Trigger
IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
	GlobalGT("_bIMLoveTalk","GLOBAL",0)
	Global("_bIMBathTalk","GLOBAL",0)
	OR(4)
		AreaCheck("AR1602") // Brynnlaw - Brynnlaw Inn
		AreaCheck("AR2202") //Ust Natha Inn 1st Floor
		AreaCheck("AR2203") //Ust Natha Inn 2nd Floor
		Detect("udsvir04") //Grey gnome innkeeper in Underdark
THEN
	RESPONSE #100
	SetGlobal("_bIMBathTalk","GLOBAL",1)
	SetGlobal("_brestcheck","GLOBAL",2)
END

IF
	Global("_bIMBathTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//3rd Talk Trigger
IF
	InParty("Imoen2") 
	InParty("Mazzy") 
	Global("_bIMLoveTalk","GLOBAL",5)
THEN
	RESPONSE #100
	SetGlobal("_bIMLoveTalk","GLOBAL",6)
	SetGlobalTimer("_bImMaz2","GLOBAL",TWO_DAYS)
END

IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
	Global("_bIMLoveTalk","GLOBAL",6)
	GlobalTimerExpired("_bImMaz2","GLOBAL")
	!Dead("c6Bodhi")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bIMLoveTalk","GLOBAL",7)
END

IF
	Global("_bIMLoveTalk","GLOBAL",7)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Drow Talk Trigger
IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
	Global("_bIMDrowTalk","GLOBAL",1)
	Global("PlayerLooksLikeDrow","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Post CN Sex Talk Trigger (Imoen Romance)
IF
	InParty("Imoen2")  
	InParty("Mazzy") 
	Global("MRSleptImoen","GLOBAL",1)
	OR(2) 
		Global("_bImoenMazzyRomance","GLOBAL",1)
		Global("_bImoenMazzyRomance","GLOBAL",2)
	Global("_bIMCNSexTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bIMCNSexTalk","GLOBAL",1)
	SetGlobalTimer("_bImMaz4","GLOBAL",ONE_HOUR)
END

IF
	IfValidForPartyDialogue("Imoen2")  
	IfValidForPartyDialogue("Mazzy")
	Global("_bIMCNSexTalk","GLOBAL",1)
	GlobalTimerExpired("_bImMaz4","GLOBAL")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bIMCNSexTalk","GLOBAL",2)
END

IF
	Global("_bIMCNSexTalk","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Post CN Sex Talk Trigger (Mazzy Romance)
IF
	InParty("Imoen2")  
	InParty("Mazzy") 
	OR(2)
		Global("_bImoenMazzyRomance","GLOBAL",1)
		Global("_bImoenMazzyRomance","GLOBAL",2)
	Global("_bIMCNSexTalk2","GLOBAL",0)
	Global("_bMazzyRomance","GLOBAL",2)
	OR(2)
	Global("ImoenRomanceActive","GLOBAL",0)
	Global("ImoenRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bIMCNSexTalk2","GLOBAL",1)
	SetGlobalTimer("_bImMaz5","GLOBAL",TWO_HOURS)
END

IF
	IfValidForPartyDialogue("Imoen2")  
	IfValidForPartyDialogue("Mazzy")
	Global("_bIMCNSexTalk2","GLOBAL",1)
	GlobalTimerExpired("_bImMaz5","GLOBAL")
	Global("_bMazzyRomance","GLOBAL",2)
	CombatCounter(0)
	OR(2)
		Global("ImoenRomanceActive","GLOBAL",0)
		Global("ImoenRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bIMCNSexTalk2","GLOBAL",2)
END

IF
	Global("_bIMCNSexTalk2","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Mazzy & Imoen Kiss 
IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
	Global("_bImoenMazzyRomance","GLOBAL",1)
	Global("_bIMKiss","GLOBAL",0)
	Dead("c6Bodhi")
	AreaType(OUTDOOR)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bIMKiss","GLOBAL",1)
END

IF
	Global("_bIMKiss","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Mazzy/CN 3Some Talk
IF
	InParty("Imoen2")
	InParty("Mazzy") 
	Global("_bMazzyRomance","GLOBAL",2)
	Global("ImoenRomanceActive","GLOBAL",2)
	Global("_bImoenMazzyRomance","GLOBAL",2)
	Global("_bIM3Some","GLOBAL",0)
	GlobalTimerExpired("_bMazSexTimer","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bIM3Some","GLOBAL",1)
	SetGlobalTimer("_bIM3SomeTimer","GLOBAL",THREE_DAYS)
END

IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
	Global("_bMazzyRomance","GLOBAL",2)
	Global("ImoenRomanceActive","GLOBAL",2)
	Global("_bIM3Some","GLOBAL",1)
	GlobalTimerExpired("_bIM3SomeTimer","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bIM3Some","GLOBAL",2)
END

IF
	Global("_bIM3Some","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Mazzy-Imoen Rasaad Talk
IF
	InParty("Imoen2")
	InParty("Mazzy")
	InParty("RASAAD")
	Global("_bImoenMazzyRomance","GLOBAL",2)
	Global("_bIMRTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bIMRTalk","GLOBAL",1)
	SetGlobalTimer("_bIMRTimer","GLOBAL",ONE_DAY)
END

IF
	IfValidForPartyDialogue("Imoen2") 
	IfValidForPartyDialogue("Mazzy") 
	InParty("RASAAD")
	Global("_bImoenMazzyRomance","GLOBAL",2)
	Global("_bIMRTalk","GLOBAL",1)
	GlobalTimerExpired("_bIMRTimer","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bIMRTalk","GLOBAL",2)
END

IF
	Global("_bIMRTalk","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Retroactive Imoen Romance 2 if they've kissed already.
IF
	GlobalGT("_bIMKiss","GLOBAL",1)
	Global("_bImoenMazzyRomance","GLOBAL",1)
	Global("_bImoenMazzyRomance2Fix","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bImoenMazzyRomance","GLOBAL",2)
	SetGlobal("_bImoenMazzyRomance2Fix","GLOBAL",1)
END

//Imoen wears sex change belt.
IF
HasItemEquiped("_BBELT2",Myself)
Gender(Myself,FEMALE)
Global("_bImoenGoesMale","GLOBAL",0)
THEN
	RESPONSE #100
CreateVisualEffectObject("SPPOLYMP",Myself)
ChangeGender(Myself,MALE)
Polymorph(THIEF_MALE_HALFLING)
SetGlobal("_bImoenGoesMale","GLOBAL",1)
END

IF
!HasItemEquiped("_BBELT2",Myself)
Gender(Myself,MALE)
Global("_bImoenGoesMale","GLOBAL",1)
THEN
	RESPONSE #100
CreateVisualEffectObject("SPPOLYMP",Myself)
ChangeGender(Myself,FEMALE)
Polymorph(THIEF_FEMALE_HALFLING)
SetGlobal("_bImoenGoesMale","GLOBAL",0)
END

//CN Permission Talk for Courting Mazzy
IF
	IfValidForPartyDialogue("Imoen2")
	IfValidForPartyDialogue("Mazzy")
	GlobalGT("_bIMLoveTalk","GLOBAL",4)
	Global("_bIMTBTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bIMTBTalk","GLOBAL",1)
END

IF
	Global("_bIMTBTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//1st Wizard Wings Talk - 1st trigger in Aerie
IF
	Global("_bImoenSaysPolymorph","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//2nd Aerie Wizard Talk
IF
	InParty("Imoen2")
	InParty("Aerie") 
	Global("_bImoenSaysPolymorph","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobalTimer("_IAWizWings","GLOBAL",FIVE_DAYS)
	SetGlobal("_bImoenSaysPolymorph","GLOBAL",3)
END

IF
	IfValidForPartyDialogue("Imoen2")
	IfValidForPartyDialogue("Aerie") 
	Global("_bImoenSaysPolymorph","GLOBAL",3)
	GlobalTimerExpired("_IAWizWings","GLOBAL")
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bImoenSaysPolymorph","GLOBAL",4)
END

IF
	Global("_bImoenSaysPolymorph","GLOBAL",4)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
