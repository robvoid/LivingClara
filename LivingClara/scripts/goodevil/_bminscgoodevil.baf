IF	//Trigger for Minsc leaving the party forever.
	Global("_bMinscLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END


//1st Trigger for Mazzy talk
IF
	GlobalGT("MinscMazzy","LOCALS",0)
	Global("_bMazzyMinscTalk","GLOBAL",0)
	InParty("Minsc")
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobalTimer("_bMazzyMinscTime","GLOBAL",ONE_DAY)
	SetGlobal("_bMazzyMinscTalk","GLOBAL",1)
END


//1st Trigger for Mazzy-Jan talk
IF
	GlobalGT("_bMazzyMinscTalk","GLOBAL",2)
	Global("_bMazzyMinscJanTalk","GLOBAL",0)
	InParty("Minsc")
	InParty("Mazzy")
	InParty("Jan")
THEN
	RESPONSE #100
	SetGlobalTimer("_bMazzyMinscJanTime","GLOBAL",TWO_DAYS)
	SetGlobal("_bMazzyMinscJanTalk","GLOBAL",1)
END

//Code for 5th Minsc-Neera talk
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bNeeraMinscLove","GLOBAL",12)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bNMLoveTime","GLOBAL",FOUR_DAYS)
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bNMLoveTime","GLOBAL")
	Global("_bNeeraMinscLove","GLOBAL",13)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bNeeraMinscLove","GLOBAL",14)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END


//Aerie Wing Reponse
IF
	Global("_bAerieWingResponse","GLOBAL",1)
	InParty("Minsc")
	!StateCheck("Minsc",CD_STATE_NOTVALID)
	Global("_bMinscWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Trigger for first Mazzy-Minsc Banter
IF
	IfValidForPartyDialog("Minsc")
	IfValidForPartyDialog("Mazzy")
	HPLT(Myself,20)
	Global("MinscMazzy","LOCALS",0)
THEN
	RESPONSE #100
	Interact("Mazzy")
END

