IF	//Trigger for first conversation if it never happened in SOA. This should hopefully work properly. If in middle of chain, should continue.
	AreaCheck("AR4500") //Pocket Plane
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	Global("_bedwinmazzy","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bedwinmazzy","GLOBAL",1)
	SetGlobalTimer("_bedwinmazzy1","GLOBAL",THREE_DAYS)
END

IF	Global("_bedwinmazzy","GLOBAL",1)
	GlobalTimerExpired("_bedwinmazzy1","GLOBAL")
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bedwinmazzy","GLOBAL",2)
END

IF
	Global("_bedwinmazzy","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Trigger for Breakup Talk. 3 hours to give time for research
IF
	Global("EdwinRomanceActive","GLOBAL",3) 
	Global("_bMazNotEdwin","GLOBAL",1)
	InParty("Edwin") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobalTimer("_bEdwinBreakUp","GLOBAL",THREE_HOURS)
	SetGlobal("_bMazNotEdwin","GLOBAL",2)
END

IF
	GlobalTimerExpired("_bEdwinBreakUp","GLOBAL") 
	Global("_bMazNotEdwin","GLOBAL",2)
	InParty("Edwin") 
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazNotEdwin","GLOBAL",3)
END

IF
	Global("_bMazNotEdwin","GLOBAL",3)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END


//Trigger for Edwin/Aerie Wing Cutscene
IF
	InParty("Edwin")
	InParty("Aerie")
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	Global("_bEdwinAerieWings","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobalTimer("_bAWingTimer","GLOBAL",TEN_DAYS)
	SetGlobal("_bEdwinAerieWings","GLOBAL",1)
END

IF 
	InParty("Edwin")
	InParty("Aerie")
	Global("_bEdwinAerieWings","GLOBAL",1)
	GlobalTimerExpired("_bAWingTimer","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bEdwinAerieWings","GLOBAL",2)
END

IF
	InParty("Edwin")
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Aerie")
	!StateCheck("Aerie",CD_STATE_NOTVALID)
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	Global("_bEdwinAerieWings","GLOBAL",2)
	AreaType(OUTDOOR)
	CombatCounter(0)
THEN
	RESPONSE #100
	FadeToColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",3)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",3)
THEN
	RESPONSE #100
	Polymorph(MAGE_FEMALE_ELF)
	ApplySpellRES("_bELFP","Edwin")
	GiveItemCreate("_BWING","Edwin",0,0,0)
	XEquipItem("_BWING","Edwin",SLOT_HELMET,EQUIP)
	SetGlobal("_bEdwinAerieWings","GLOBAL",4)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",4)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",6)
THEN
	RESPONSE #100
	CreateVisualEffectObject("SPPOLYMP","Aerie")
	Wait(1)
	FadeToColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",7)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",7)
THEN
	RESPONSE #100
	ApplySpellRES("_BEDWP","Edwin")
	Polymorph(MAGE_MALE_HUMAN)
	DestroyItem("_BWING")
	ApplySpellRES("_bAERP","Aerie")
	ActionOverride("Aerie",DestroyItem("_BWING"))
	SetGlobal("_bEdwinAerieWings","GLOBAL",8)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",8)
THEN
	RESPONSE #100
	FadeFromColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",9)
END


//TOB Only polymorph
IF
	Global("_bEdwinAerieWings","GLOBAL",9)
	!PartyHasItem("_BWINGA")
THEN
	RESPONSE #100
	SetGlobalTimer("_bAWingTimer2","GLOBAL",THREE_DAYS)
	SetGlobal("_bEdwinAerieWings","GLOBAL",10)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",10)
	GlobalTimerExpired("_bAWingTimer2","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bEdwinAerieWings","GLOBAL",11)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",11)
	InParty("Edwin")
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	InParty("Aerie")
	!StateCheck("Aerie",CD_STATE_NOTVALID)
	!PartyHasItem("_BWINGA")
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	CombatCounter(0)
THEN
	RESPONSE #100
	FadeToColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",12)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",12)
THEN
RESPONSE #100
	Polymorph(MAGE_FEMALE_ELF)
	ApplySpellRES("_bAERP","Edwin")
	GiveItemCreate("_BWING","Edwin",0,0,0)
	XEquipItem("_BWING","Edwin",SLOT_HELMET,EQUIP)
	SetGlobal("_bEdwinAerieWings","GLOBAL",13)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",13)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Code to change Edwin back after TOB talk. Next talk trigger in Aerie
IF
	Global("_bEdwinAerieWings","GLOBAL",14)
THEN
	RESPONSE #100
	FadeToColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",15)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",15)
THEN
	RESPONSE #100
	ApplySpellRES("_BEDWP","Edwin")
	Polymorph(MAGE_MALE_HUMAN)
	DestroyItem("_BWING")
	SetGlobal("_bEdwinAerieWings","GLOBAL",16)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",16)
THEN
	RESPONSE #100
	FadeFromColor([20.0],0)
	SetGlobal("_bEdwinAerieWings","GLOBAL",17)
END


//Edwin wears Sex change belt
IF
HasItemEquiped("_BBELTE",Myself)
Gender(Myself,MALE)
Global("_bEdwinGoesFemale","GLOBAL",0)
Global("_bEdwinGoesMale","GLOBAL",0)
THEN
	RESPONSE #100
	CreateVisualEffectObject("SPPOLYMP",Myself)
	ChangeGender(Myself,FEMALE)
	Polymorph(MAGE_FEMALE_HUMAN)
	SetGlobal("_bEdwinGoesFemale","GLOBAL",1)
END

IF
	!HasItemEquiped("_BBELTE",Myself)
	Gender(Myself,FEMALE)
	Global("_bEdwinGoesFemale","GLOBAL",1)
THEN
	RESPONSE #100
	CreateVisualEffectObject("SPPOLYMP",Myself)
	ChangeGender(Myself,MALE)
	Polymorph(MAGE_MALE_HUMAN)
	SetGlobal("_bEdwinGoesFemale","GLOBAL",0)
END

//Alternate version, just in case...
IF
	HasItemEquiped("_BBELTE",Myself)
	Gender(Myself,FEMALE)
	Global("_bEdwinGoesMale","GLOBAL",0)
	Global("_bEdwinGoesFemale","GLOBAL",0)
THEN
	RESPONSE #100
	CreateVisualEffectObject("SPPOLYMP",Myself)
	ChangeGender(Myself,MALE)
	Polymorph(MAGE_MALE_HUMAN)
	SetGlobal("_bEdwinGoesMale","GLOBAL",1)
END

IF
	!HasItemEquiped("_BBELTE",Myself)
	Gender(Myself,MALE)
	Global("_bEdwinGoesMale","GLOBAL",1)
THEN
	RESPONSE #100
	CreateVisualEffectObject("SPPOLYMP",Myself)
	ChangeGender(Myself,FEMALE)
	Polymorph(MAGE_FEMALE_HUMAN)
	SetGlobal("_bEdwinGoesMale","GLOBAL",0)
END

//Aerie Wing Reponse
IF
	Global("_bAerieWingResponse","GLOBAL",1)
	InParty("Edwin")
	!StateCheck("Edwin",CD_STATE_NOTVALID)
	GlobalGT("_bEdwinAerieWings","GLOBAL",4)
	Global("_bEdwinWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END
