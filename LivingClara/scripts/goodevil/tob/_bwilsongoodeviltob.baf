//Aerie Wing Reponse
IF
	Global("_bAerieWingResponse","GLOBAL",1)
	InParty("WILSON")
	!StateCheck("WILSON",CD_STATE_NOTVALID)
	Global("_bWilsonWingResponse","GLOBAL",0)
	CombatCounter(0)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//First Wislon-Jaheira Romance Timer
IF
	InParty("WILSON")
	InParty("Jaheira")
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	OR(15)
		Global("_bWilsonJaheiraTalks","GLOBAL",0) //1
		Global("_bWilsonJaheiraTalks","GLOBAL",4) //2
		Global("_bWilsonJaheiraTalks","GLOBAL",7) //3
		Global("_bWilsonJaheiraTalks","GLOBAL",11) //4
		Global("_bWilsonJaheiraTalks","GLOBAL",15) //5
		Global("_bWilsonJaheiraTalks","GLOBAL",18) //6
		Global("_bWilsonJaheiraTalks","GLOBAL",22)//7
		Global("_bWilsonJaheiraTalks","GLOBAL",26)//8
		Global("_bWilsonJaheiraTalks","GLOBAL",30)//9
		Global("_bWilsonJaheiraTalks","GLOBAL",34)//10
		Global("_bWilsonJaheiraTalks","GLOBAL",38)//11
		Global("_bWilsonJaheiraTalks","GLOBAL",42)//12
		Global("_bWilsonJaheiraTalks","GLOBAL",46)//13
		Global("_bWilsonJaheiraTalks","GLOBAL",50)//14
		Global("_bWilsonJaheiraTalks","GLOBAL",54)//15
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",TWO_DAYS)
END

//First Wilson-Jaheira Romance Timer. No need to separate these, I just did since I was copying from SoA.
IF
	InParty("WILSON")
	InParty("Jaheira")
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	OR(9)
		Global("_bWilsonJaheiraTalks","GLOBAL",58)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",62)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",66)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",70)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",74)//5
		Global("_bWilsonJaheiraTalks","GLOBAL",78)//6
		Global("_bWilsonJaheiraTalks","GLOBAL",82)//7
		Global("_bWilsonJaheiraTalks","GLOBAL",85)//8
		Global("_bWilsonJaheiraTalks","GLOBAL",89)//9
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",TWO_DAYS)
END

//Timer for 25th/26th talks. No need to separate these, I just did since I was copying from SoA.
IF
	InParty("WILSON")
	InParty("Jaheira")
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	OR(2)
		Global("_bWilsonJaheiraTalks","GLOBAL",93)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",97)//2
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",TWO_DAYS)
END

//Second Timer if normal trigger requirements
IF
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	OR(20)
		Global("_bWilsonJaheiraTalks","GLOBAL",8)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",12)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",19)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",23)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",27)//5
		Global("_bWilsonJaheiraTalks","GLOBAL",31)//6
		Global("_bWilsonJaheiraTalks","GLOBAL",35)//7
		Global("_bWilsonJaheiraTalks","GLOBAL",39)//8
		Global("_bWilsonJaheiraTalks","GLOBAL",43)//9
		Global("_bWilsonJaheiraTalks","GLOBAL",47)//10
		Global("_bWilsonJaheiraTalks","GLOBAL",51)//11
		Global("_bWilsonJaheiraTalks","GLOBAL",55)//12
		Global("_bWilsonJaheiraTalks","GLOBAL",59)//13
		Global("_bWilsonJaheiraTalks","GLOBAL",63)//14
		Global("_bWilsonJaheiraTalks","GLOBAL",67)//15
		Global("_bWilsonJaheiraTalks","GLOBAL",71)//16
		Global("_bWilsonJaheiraTalks","GLOBAL",75)//17
		Global("_bWilsonJaheiraTalks","GLOBAL",79)//18
		Global("_bWilsonJaheiraTalks","GLOBAL",86)//19
		Global("_bWilsonJaheiraTalks","GLOBAL",90)//20
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THIRTY_MINUTES)
END

//Second Timer if in City
IF
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	OR(1)
		Global("_bWilsonJaheiraTalks","GLOBAL",1)//1
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
	AreaType(CITY)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THIRTY_MINUTES)
END

//2nd Trigger if Party Rested- No 2nd Timer
IF
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	OR(2)
		Global("_bWilsonJaheiraTalks","GLOBAL",5)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",16)//2
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	PartyRested()
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
END

//2nd trigger, outside and morning. No 3rd trigger.
IF
	Global("_bWilsonJaheiraTalks","GLOBAL",83)
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	CombatCounter(0)
	AreaType(OUTDOOR)
	TimeOfDay(MORNING)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
END

//2nd trigger, outside and not in a city. No need to separate these, but I'm copying from SoA so I did.
IF
	OR(2)
		Global("_bWilsonJaheiraTalks","GLOBAL",94)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",98)//2. 3rd trigger in Jaheiradgoodevil.baf
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	CombatCounter(0)
	AreaType(OUTDOOR)
	!AreaType(CITY)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THIRTY_MINUTES)
END

//3rd Trigger if no requirements.
IF
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	OR(20)
		Global("_bWilsonJaheiraTalks","GLOBAL",9)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",13)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",20)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",24)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",28)//5
		Global("_bWilsonJaheiraTalks","GLOBAL",32)//6
		Global("_bWilsonJaheiraTalks","GLOBAL",36)//7
		Global("_bWilsonJaheiraTalks","GLOBAL",40)//8
		Global("_bWilsonJaheiraTalks","GLOBAL",44)//9
		Global("_bWilsonJaheiraTalks","GLOBAL",48)//10
		Global("_bWilsonJaheiraTalks","GLOBAL",52)//11
		Global("_bWilsonJaheiraTalks","GLOBAL",56)//12
		Global("_bWilsonJaheiraTalks","GLOBAL",60)//13
		Global("_bWilsonJaheiraTalks","GLOBAL",64)//14
		Global("_bWilsonJaheiraTalks","GLOBAL",68)//15
		Global("_bWilsonJaheiraTalks","GLOBAL",72)//16
		Global("_bWilsonJaheiraTalks","GLOBAL",76)//17
		Global("_bWilsonJaheiraTalks","GLOBAL",80)//18
		Global("_bWilsonJaheiraTalks","GLOBAL",87)//19
		Global("_bWilsonJaheiraTalks","GLOBAL",91)//20
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
END

//3rd Trigger if in City
IF
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	OR(1)
		Global("_bWilsonJaheiraTalks","GLOBAL",2)//1
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	AreaType(CITY)
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
END

//3rd trigger, outside and not in a city. Dermin showed himself to be evil.
IF
	Global("_bWilsonJaheiraTalks","GLOBAL",95)
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	CombatCounter(0)
	AreaType(OUTDOOR)
	!AreaType(CITY)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
END
	

//Dialogue Start Trigger if Wilson talks first (if Jaheira speaks first it's in Jaheiragoodevil.baf)
IF
	OR(8)
		Global("_bWilsonJaheiraTalks","GLOBAL",3)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",10)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",21)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",37)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",53)//5
		Global("_bWilsonJaheiraTalks","GLOBAL",61)//6
		Global("_bWilsonJaheiraTalks","GLOBAL",73)//7
		Global("_bWilsonJaheiraTalks","GLOBAL",96)//8
THEN
	RESPONSE #100
	PlaySong(36)
	StartDialogNoSet(Player1)
END

//Script for 1st ToB only talk, 1st timer.
IF
	InParty("WILSON")
	InParty("Jaheira")
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
		Global("_bWilsonJaheiraTalks","GLOBAL",104)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THREE_DAYS)
END

//Second Timer for ToB only talks if normal trigger requirements
IF
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	OR(5)
		Global("_bWilsonJaheiraTalks","GLOBAL",105)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",109)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",115)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",119)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",123)//5
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THIRTY_MINUTES)
END

//3rd Trigger for ToB only talks if no requirements.
IF
	GlobalTimerExpired("_bWilsonJaheiraTimer","GLOBAL")
	OR(5)
		Global("_bWilsonJaheiraTalks","GLOBAL",106)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",110)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",116)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",120)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",124)//5
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
END

//Post-wraith talk. (Next trigger in jaheiragoodeviltob.baf)
IF
	Global("_bJWWraith","GLOBAL",2)
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	IfValidForPartyDialogue("WILSON")
	IfValidForPartyDialogue("Jaheira")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bJWWraith","GLOBAL",3)
END

//Talk #2. Requires talk 1 and post-wraith talk.
IF
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
	Global("_bJWWraith","GLOBAL",4)
	Global("_bWilsonJaheiraTalks","GLOBAL",108)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THREE_DAYS)
END

//Talk #3. Requires Saradush being sacked and talk #2. Next trigger in Jaheiragoodeviltob.baf
IF
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
	Global("_bWilsonJaheiraTalks","GLOBAL",112)
	Global("SaradushSacked","GLOBAL",1)
	PartyRested()
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
END

//Talk #4. Requires Talk #3 and also meeting Balthazar. 
IF
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
	GlobalGT("METBAL","GLOBAL",0)
	Global("_bWilsonJaheiraTalks","GLOBAL",114)
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THREE_DAYS)
END

//Talk #5. Requires talk #4 and also killing Sendai or Abazigal.
IF
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
	Global("_bWilsonJaheiraTalks","GLOBAL",118)
	OR(2)
		Dead("sendai")
		Dead("abazigal")
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THREE_DAYS)
END

//Talk #6. Requires talk #5 and also killing both Sendai and Abazigal.
IF
	!Global("JaheiraRomanceActive","GLOBAL",1)
	!Global("JaheiraRomanceActive","GLOBAL",2)
	!Global("_bWilsonJaheira","GLOBAL",3)
	InParty("WILSON")
	InParty("Jaheira")
	Global("_bWilsonJaheiraTalks","GLOBAL",122)
	Dead("sendai")
	Dead("abazigal")
THEN
	RESPONSE #100
	IncrementGlobal("_bWilsonJaheiraTalks","GLOBAL",1)
	SetGlobalTimer("_bWilsonJaheiraTimer","GLOBAL",THREE_DAYS)
END

	