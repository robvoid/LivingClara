IF	//Trigger for Keldorn leaving the party forever.
	Global("_bKeldLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END


//Trigger for 1st talk about Viconia
IF
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty("Viconia")
	Global("_bmazkelvic","GLOBAL",0)
	OR(2)
	Global("MazzyVicFriends","GLOBAL",1)
	Global("MazzyVicFriends","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bmazkelvic","GLOBAL",1)
END

IF
	Global("_bmazkelvic","GLOBAL",1)
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Mazzy") 
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty("Viconia")
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Second Talk about Viconia
IF
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	Global("_bmazkelvic","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_bmazkelvic","GLOBAL",3)
	SetGlobalTimer("_bMKVTimer1","GLOBAL",THREE_DAYS)
END

IF
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	Global("_bmazkelvic","GLOBAL",3)
	GlobalTimerExpired("_bMKVTimer1","GLOBAL")
THEN
	RESPONSE #100
	SetGlobal("_bmazkelvic","GLOBAL",4)
END

IF
	Global("_bmazkelvic","GLOBAL",4)
	InParty("Keldorn") 
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
