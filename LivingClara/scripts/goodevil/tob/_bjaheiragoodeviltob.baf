
//Jaheira/Aerie Wing talk, next trigger in Aerie
IF
	IfValidForPartyDialog("Aerie") 
	IfValidForPartyDialog("Jaheira")
	Global("_bAerieJahWingTalk","GLOBAL",0)
	HaveKnownSpell(CLERIC_REGENERATE)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bAerieJahWingTalk","GLOBAL",1)
END

//2nd Mazzy talk 
IF
	Global("_BJahMazFriends","GLOBAL",1)
	Global("_bJMComfort","GLOBAL",0)
	InParty("Jaheira") 
	InParty("Mazzy") 
THEN
	RESPONSE #100
	SetGlobal("_bJMComfort","GLOBAL",1)
	SetGlobalTimer("_bmjtimer2","GLOBAL",THREE_DAYS)
END

IF
	Global("_BJahMazFriends","GLOBAL",1)
	Global("_bJMComfort","GLOBAL",1)
	GlobalTimerExpired("_bmjtimer2","GLOBAL")
	IfValidForPartyDialog("Jaheira") 
	IfValidForPartyDialog("Mazzy") 
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bJMComfort","GLOBAL",2)
END

IF
	Global("_bJMComfort","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Mazzy Romance Breakup Talk
IF
	Global("_BJahMazFriends","GLOBAL",1)
	Global("_bMazNotJaheira","GLOBAL",1)
	Global("_bJMBreakUp","GLOBAL",0)
	InParty("Jaheira") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobal("_bJMBreakUp","GLOBAL",1)
	SetGlobalTimer("_bmjtimer3","GLOBAL",ONE_HOUR)
END

IF
	Global("_bJMBreakUp","GLOBAL",1)
	IfValidForPartyDialog("Jaheira") 
	IfValidForPartyDialog("Mazzy")
	GlobalTimerExpired("_bmjtimer3","GLOBAL")
	Global("_bMazNotJaheira","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bJMBreakUp","GLOBAL",2)
END

IF
	Global("_bJMBreakUp","GLOBAL",2)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END

//Wilson-Jaheira Start Dialogue Trigger if Jaheira talks first.
IF
	OR(19)
		Global("_bWilsonJaheiraTalks","GLOBAL",6)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",14)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",17)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",25)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",29)//5
		Global("_bWilsonJaheiraTalks","GLOBAL",33)//6
		Global("_bWilsonJaheiraTalks","GLOBAL",41)//7
		Global("_bWilsonJaheiraTalks","GLOBAL",45)//8
		Global("_bWilsonJaheiraTalks","GLOBAL",49)//9
		Global("_bWilsonJaheiraTalks","GLOBAL",57)//10
		Global("_bWilsonJaheiraTalks","GLOBAL",65)//11
		Global("_bWilsonJaheiraTalks","GLOBAL",69)//12
		Global("_bWilsonJaheiraTalks","GLOBAL",77)//13
		Global("_bWilsonJaheiraTalks","GLOBAL",81)//14
		Global("_bWilsonJaheiraTalks","GLOBAL",84)//15
		Global("_bWilsonJaheiraTalks","GLOBAL",88)//16
		Global("_bWilsonJaheiraTalks","GLOBAL",92)//17
		Global("_bWilsonJaheiraTalks","GLOBAL",100)//18
		Global("_bWilsonJaheiraTalks","GLOBAL",102)//19
THEN
	RESPONSE #100
	PlaySong(36)
	StartDialogNoSet(Player1)
END

//Wilson-Jaheira Start Dialogue Trigger if Jaheira talks first for ToB only talks.
IF
	OR(6)
		Global("_bWilsonJaheiraTalks","GLOBAL",107)//1
		Global("_bWilsonJaheiraTalks","GLOBAL",111)//2
		Global("_bWilsonJaheiraTalks","GLOBAL",113)//3
		Global("_bWilsonJaheiraTalks","GLOBAL",117)//4
		Global("_bWilsonJaheiraTalks","GLOBAL",121)//5
		Global("_bWilsonJaheiraTalks","GLOBAL",125)//6
THEN
	RESPONSE #100
	PlaySong(36)
	StartDialogNoSet(Player1)
END

//Dialogue trigger for post-wraith scene (previous trigger in _bwilsongoodeviltob.baf)
IF
	Global("_bJWWraith","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


