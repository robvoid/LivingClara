IF	//Trigger for HaerDalis leaving the party forever.
	Global("_bHDLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END



//Dorn-Aerie Conflict Trigger (Here because it's HD's local), the Dorn first version
IF 
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	IfValidForPartyDialog("Aerie")
	GlobalGT("_bDornHDLove","GLOBAL",12)  //Have gotten to at least the 4th talk
	!Global("_bHDDORNROMANCE","GLOBAL",3)  //Romance not turned off
	GlobalGT("LoveTalk","LOCALS",11)   //Have gotten this far in the Aerie-HD love talks (technically possible depending on banters
	!Global("HaerDalisRomanceActive","GLOBAL",1)
	!Global("HaerDalisRomanceActive","GLOBAL",2)
	Global("_bAerieDornConflict","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDAerieTimer","GLOBAL",FIVE_HOURS)
	IncrementGlobal("_bAerieDornConflict","GLOBAL",1)
END

IF  //The Aerie first version
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	IfValidForPartyDialog("Aerie")
	GlobalGT("_bDornHDLove","GLOBAL",10)  //Have gotten to at least the 3rd talk
	!Global("_bHDDORNROMANCE","GLOBAL",3)  //Romance not turned off
	OR(2)
	Global("HaerDalisRomanceActive","GLOBAL",1)  //Aerie & HD are committed
	Global("HaerDalisRomanceActive","GLOBAL",2)
	Global("_bAerieDornConflict","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDAerieTimer","GLOBAL",FIVE_HOURS)
	IncrementGlobal("_bAerieDornConflict","GLOBAL",1)
END


//Trigger for Post-Rest Dorn Talk (1st trigger in Dorn b/c easier)
IF
	Global("_bDornHDLove","GLOBAL",22)
THEN	
	RESPONSE #100
	PlaySong(0)   
	PlaySong(87)  
	StartDialogNoSet(Player1)
END

//Romance Combat Talk - Dorn Injured
IF
	Global("_bHDDORNROMANCE","GLOBAL",2)
	HPPercentLT("Dorn",25)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDornHurt","GLOBAL",0)
	OR(2)
	Global("_bHDHurt","GLOBAL",0)
	Global("_bHDHurt","GLOBAL",3)
	ActuallyInCombat()
THEN 
	RESPONSE #100
	SetGlobal("_bDornHurt","GLOBAL",1)
END

IF
	Global("_bDornHurt","GLOBAL",1)
	HPPercentLT("Dorn",25)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	OR(2)
	Global("_bHDHurt","GLOBAL",0)
	Global("_bHDHurt","GLOBAL",3)
	ActuallyInCombat()
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_bDornHurt","GLOBAL",2)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bDornHurt","GLOBAL",3)
END	

//Joining if Dorn is fighting CN
IF
	!InParty("Dorn")
	Allegiance("Dorn",ENEMY)
	Detect("Dorn")
	Global("_bHDJoinsDorn","GLOBAL",0)
	Global("_bHDDORNROMANCE","GLOBAL",2)
	IfValidForPartyDialog("HaerDalis")
THEN
	RESPONSE #100
	SetGlobal("_bHDJoinsDorn","GLOBAL",1)
END

IF
	Global("_bHDJoinsDorn","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Trigger for Post Dorn Quest Talk (2nd trigger in d script)
IF
	Global("OHD_homeagain_dorn","OH5400",2) //After Dorn's post quest talk
	Global("_bHDDORNROMANCE","GLOBAL",2) //Romance active
	Global("_bDornHDTOBQuest","GLOBAL",0)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
THEN
	RESPONSE #100
	SetGlobal("_bDornHDTOBQuest","GLOBAL",1)
END

IF
	Global("_bDornHDTOBQuest","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
	