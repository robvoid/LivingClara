IF	//Trigger for Neera leaving the party forever.
	Global("_bNeeraLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2) 
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END


//Code for Neera-Minsc Romance. All the talks with the same timer. Shorter for TOB
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(7)
	Global("_bNeeraMinscLove","GLOBAL",0)
	Global("_bNeeraMinscLove","GLOBAL",3)
	Global("_bNeeraMinscLove","GLOBAL",6)
	Global("_bNeeraMinscLove","GLOBAL",9)
	Global("_bNeeraMinscLove","GLOBAL",15)
	Global("_bNeeraMinscLove","GLOBAL",18)
	Global("_bNeeraMinscLove","GLOBAL",21)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bNMLoveTime","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bNMLoveTime","GLOBAL")
	OR(7)
	Global("_bNeeraMinscLove","GLOBAL",1)
	Global("_bNeeraMinscLove","GLOBAL",4)
	Global("_bNeeraMinscLove","GLOBAL",7)
	Global("_bNeeraMinscLove","GLOBAL",10)
	Global("_bNeeraMinscLove","GLOBAL",16)
	Global("_bNeeraMinscLove","GLOBAL",19)
	Global("_bNeeraMinscLove","GLOBAL",22)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(7)
	Global("_bNeeraMinscLove","GLOBAL",2)
	Global("_bNeeraMinscLove","GLOBAL",5)
	Global("_bNeeraMinscLove","GLOBAL",8)
	Global("_bNeeraMinscLove","GLOBAL",11)
	Global("_bNeeraMinscLove","GLOBAL",17)
	Global("_bNeeraMinscLove","GLOBAL",20)
	Global("_bNeeraMinscLove","GLOBAL",23)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Separate City Talk - only requires first talk
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	GlobalGT("_bMinscNeera","GLOBAL",0)
	Global("_bMNCityTalk","GLOBAL",0)
	OR(2)
		AreaCheck("AR5500")
		AreaType(CITY)
	AreaType(OUTDOOR)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bMNCityTalk","GLOBAL",1)
END

IF
	Global("_bMNCityTalk","GLOBAL",1)
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Separate Inn Talk - only requires first one
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	GlobalGT("_bMinscNeera","GLOBAL",0)
	Global("_bMNInnTalk","GLOBAL",0)
	OR(2)
		AreaCheck("AR5003")        // Saradush Tavern
		AreaCheck("AR5501")	      //Amkethran Inn 
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bMNInnTalk","GLOBAL",1)
END

IF
	Global("_bMNInnTalk","GLOBAL",1)
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//1st Romance Talk - Requires other City Talk & 1st 8 Talks
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	Global("_bNeeraMinscLove","GLOBAL",24)
	Global("_bMNCityTalk","GLOBAL",2)
	OR(2)
		AreaCheck("AR5500")
		AreaType(CITY)
	AreaType(OUTDOOR)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bMNCityTalk","GLOBAL",3)
END

IF
	Global("_bMNCityTalk","GLOBAL",3)
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//2nd Talk Sequence, once actually romancing
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bMinscNeera","GLOBAL",2)
	OR(4)
	Global("_bNeeraMinscLove","GLOBAL",24)
	Global("_bNeeraMinscLove","GLOBAL",27)
	Global("_bNeeraMinscLove","GLOBAL",32)
	Global("_bNeeraMinscLove","GLOBAL",37)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bNMLoveTime","GLOBAL",ONE_DAY)
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bNMLoveTime","GLOBAL")
	OR(4)
	Global("_bNeeraMinscLove","GLOBAL",25)
	Global("_bNeeraMinscLove","GLOBAL",28)
	Global("_bNeeraMinscLove","GLOBAL",33)
	Global("_bNeeraMinscLove","GLOBAL",38)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(4)
	Global("_bNeeraMinscLove","GLOBAL",26)
	Global("_bNeeraMinscLove","GLOBAL",29)
	Global("_bNeeraMinscLove","GLOBAL",34)
	Global("_bNeeraMinscLove","GLOBAL",39)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Combat Talk, separate trigger
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bMinscNeera","GLOBAL",2)
	ActuallyInCombat()
	Global("_bMNFightTalk","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_bMNFightTalk","GLOBAL",1)
END

IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	Global("_bMNFightTalk","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Post-rest triggers
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(2)
	Global("_bNeeraMinscLove","GLOBAL",30) //Post sex
	Global("_bNeeraMinscLove","GLOBAL",35) //Post story promise
	Global("_bMinscNeera","GLOBAL",2)
	PartyRested()
	CombatCounter(0)
THEN
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	OR(2)
	Global("_bNeeraMinscLove","GLOBAL",31)
	Global("_bNeeraMinscLove","GLOBAL",36)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END

//Trigger for Boo Cutscene Talk (1st trigger in d script)
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	Global("_bBooChange","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END


//Trigger for TOB Talk Post Quest
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	Global("_bMinscNeera","GLOBAL",2)
	Global("OHN_TOB_PLOT","GLOBAL",15)
	Global("_bMNQuestTalk","GLOBAL",0)
	CombatCounter(0)
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bMNQuestTalk","GLOBAL",1)
END
	
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	Global("_bMNQuestTalk","GLOBAL",1)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END	


//Triggers for Post-Boo TOB talks
IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	Global("_bBooChange","GLOBAL",3)
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(2)
	Global("_bNeeraMinscLove","GLOBAL",40)
	Global("_bNeeraMinscLove","GLOBAL",43) 
THEN
	RESPONSE #100
	SetGlobalTimer("_bNMLoveTime","GLOBAL",TWO_DAYS)
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF	
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc")
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	GlobalTimerExpired("_bNMLoveTime","GLOBAL")
	OR(2)
	Global("_bNeeraMinscLove","GLOBAL",41)
	Global("_bNeeraMinscLove","GLOBAL",44)
	CombatCounter(0)
THEN 
	RESPONSE #100
	IncrementGlobal("_bNeeraMinscLove","GLOBAL",1)
END

IF
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Minsc") 
	!Global("NeeraRomanceActive","GLOBAL",2)
	!Global("NeeraRomanceActive","GLOBAL",1)
	OR(2)
	Global("_bNeeraMinscLove","GLOBAL",42)
	Global("_bNeeraMinscLove","GLOBAL",45)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySong(88)
	StartDialogNoSet(Player1)
END


//Aerie-Wings Talk - next trigger in Aerie.baf
IF 
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	IfValidForPartyDialog("NEERA")
	IfValidForPartyDialog("Aerie")
	!PartyHasItem("_BWINGA")
	Global("_b25SpellHelpsAerie","GLOBAL",0)
	Global("_bNeeraASpellTalk","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bNeeraASpellTalk","GLOBAL",1)
END

IF //Turns off 1st Clara Banter if Neera knows spell
	HaveKnownSpell(WIZARD_INVISIBILITY_10_FOOT)
	Global("_BBCLARANEERA","GLOBAL",0)
THEN
	RESPONSE #100	
	SetGlobal("_BBCLARANEERA","GLOBAL",1)
END


//Post Minsc Wraith Talk
IF
	GlobalGT("WraithPunish","GLOBAL",2) //So doesn't play until after 3rd punish talk
	IfValidForPartyDialog("Minsc")
	IfValidForPartyDialog("NEERA")
	Global("_bMinscNeeraWraith","GLOBAL",3)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMinscNeeraWraith","GLOBAL",4)
END

IF
	IfValidForPartyDialog("Minsc")
	IfValidForPartyDialog("NEERA")
	Global("_bMinscNeeraWraith","GLOBAL",4)
	CombatCounter(0)
THEN
	RESPONSE #100
	Wait(4)
	StartDialogNoSet(Player1)
END

//Breakup Talk
IF
	Global("NeeraRomanceActive","GLOBAL",3) 
	Global("_bMazNotNeera","GLOBAL",1)
	InParty("NEERA") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobalTimer("_bNeeraBreakUp","GLOBAL",THIRTY_MINUTES)
	SetGlobal("_bMazNotNeera","GLOBAL",2)
END

IF
	GlobalTimerExpired("_bNeeraBreakUp","GLOBAL") 
	Global("_bMazNotNeera","GLOBAL",2)
	IfValidForPartyDialogue("NEERA")
	IfValidForPartyDialogue("Mazzy")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazNotNeera","GLOBAL",3)
END

IF
	Global("_bMazNotNeera","GLOBAL",3)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END
