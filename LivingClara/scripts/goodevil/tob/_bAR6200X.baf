//This code is solely to turn off the existing epilogues

//Mazzy (if friendship active)

IF 
	Global("StartEndBios","AR6200",1)
	InParty("Mazzy")
	Global("MazzyBio","GLOBAL",0)
	Global("_BMazzyFriendship","GLOBAL",1)
THEN
RESPONSE #100
	SetGlobal("MazzyBio","GLOBAL",3)
END


//Anomen (Darkside Anomen w/ Clara or CN)
IF
	Global("StartEndBios","AR6200",1)
	InParty("Anomen")  // Anomen
	!Dead("_bClara") //She doesn't have to be in party, just alive
	Global("_bLordAndLadyDelryn","GLOBAL",5) //Should be mutually exclusive with DS Anomen being 2
	Global("AnomenBio","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("AnomenBio","GLOBAL",3)
END

IF
	Global("StartEndBios","AR6200",1)
	InParty("Anomen")  // Anomen
	Global("_bDarksideAnomen","GLOBAL",2)
	Global("AnomenRomanceActive","GLOBAL",2) //Anomen CN Romance
	Global("AnomenBio","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("AnomenBio","GLOBAL",3)
END


//Aerie (if she has wings, or she's Minsc's witch with no CN romance)
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	InParty("Aerie")  // Aerie
	OR(2) 
		Global("PlayerChoseEssence","GLOBAL",1) //CN became a god
		!Global("AerieRomanceActive","GLOBAL",2)  //No CN romance
	Global("MinscWitch","GLOBAL",1)   //Aerie is Minsc's witch
	!Dead("Minsc")                       //Minsc isn't dead
	Global("AerieBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("AerieBio","GLOBAL",3)
END

IF
	Global("StartEndBios","AR6200",1) 
	InParty("Aerie")  // Aerie
	PartyHasItem("_BWINGA")
	Global("AerieBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("AerieBio","GLOBAL",3)
END


//Minsc (if Aerie is witch w/o CN or he's romancing Neera)
IF
	Global("StartEndBios","AR6200",1) 
	InParty("Minsc")  // Minsc
	Global("_bMinscNeera","GLOBAL",2)
	!Dead("NEERA") //doesn't have to be in party, but not dead
	Global("MinscBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("MinscBio","GLOBAL",3)
END

IF
	Global("StartEndBios","AR6200",1) 
	InParty("Minsc")  // Minsc
	OR(2) 
		Global("PlayerChoseEssence","GLOBAL",1) //CN became a god
		!Global("AerieRomanceActive","GLOBAL",2)  //or no CN romance
	Global("MinscWitch","GLOBAL",1)   //Aerie is Minsc's witch
	!Dead("Aerie")
	Global("MinscBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("MinscBio","GLOBAL",3)
END


//Neera, if she's romancing Minsc
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	Global("_bMinscNeera","GLOBAL",2)  //Romancing Minsc
	InParty("NEERA")  // Neera
	!Dead("Minsc")  //Minsc isn't dead
	Global("NeeraBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("NeeraBio","GLOBAL",3)
END

//Dorn, if romancing HaerDalis
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	Global("_bHDDORNROMANCE","GLOBAL",2)  //Romancing HD
	InParty("Dorn")  // Dorn
	!Dead("HaerDalis")  //HaerDalis isn't dead
	!Global("DornRomanceActive","GLOBAL",1)  //CN not romancing Dorn
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)  //CN not romancing HD
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	Global("OHD_bio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("OHD_bio","GLOBAL",3)
END

//HaerDalis if romancing Dorn
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	Global("_bHDDORNROMANCE","GLOBAL",2)  //Romancing Dorn
	InParty("HaerDalis")  // HaerDalis
	!Dead("Dorn")  //Dorn isn't dead
	!Global("DornRomanceActive","GLOBAL",1)  //CN not romancing Dorn
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)  //CN not romancing HD
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	Global("HaerdalisBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("HaerdalisBio","GLOBAL",3)
END

//Jaheira if romancing Wilson.
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	Global("_bWilsonJaheira","GLOBAL",2) //Romancing Wilson
	InParty("Jaheira") //Jaheira is in your party.
	!Dead("WILSON")  //Wilson isn't dead.
	Global("JaheiraBio","GLOBAL",0) 
THEN
	RESPONSE #100
	SetGlobal("JaheiraBio","GLOBAL",3)
END

//Wilson if romancing Jaheira.
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	Global("_bWilsonJaheira","GLOBAL",2) //Romancing Jaheira
	InParty("WILSON") //Wilson is in your party.
	!Dead("Jaheira") //Jaheira is not dead.
	Global("WilsonBio","GLOBAL",0) 
THEN
	RESPONSE #100
	SetGlobal("WilsonBio","GLOBAL",3)
END

//Moved from FtE
//Viconia (if no longer follows Shar, didn't try to kill everyone, and CN becomes God)
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	InParty("Viconia")  // Viconia
	!Global("_bAllRivvinMustDie","GLOBAL",1)  //Didn't poison everyone
	Global("_bMazVicConv","GLOBAL",45) //Doesn't follow Shar
	Global("PlayerChoseEssence","GLOBAL",1)   //CN becomes God
	Global("ViconiaBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("ViconiaBio","GLOBAL",3)
END

//Viconia (if no longer follows Shar, didn't try to kill everyone, no CN God, and no romance)
IF
	Global("StartEndBios","AR6200",1)  // The Throne of Bhaal
	InParty("Viconia")  // Viconia
	!Global("_bAllRivvinMustDie","GLOBAL",1)  //Didn't poison everyone
	Global("_bMazVicConv","GLOBAL",45) //Doesn't follow Shar
	Global("PlayerChoseEssence","GLOBAL",0)   //CN didn't become God
	!Global("ViconiaRomanceActive","GLOBAL",2) //no CN romance
	Global("ViconiaBio","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("ViconiaBio","GLOBAL",3)
END
