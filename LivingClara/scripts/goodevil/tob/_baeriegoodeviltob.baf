IF	//Trigger for Aerie leaving the party forever.
	Global("_bAerieLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2) 
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END


//Trigger of First Viconia talk (1st part in Viconia.baf)
IF
	IfValidForPartyDialog("Aerie") 
	IfValidForPartyDialogue("Viconia")
	Global("_bAerieVicArmTalk","GLOBAL",1)
	HPPercentLT("Viconia",50)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//2nd Talk timer
IF
	InParty("Aerie") 
	InParty("Viconia")
	Global("_bAerieVicArmTalk","GLOBAL",4)
THEN
	RESPONSE #100
	SetGlobal("_bAerieVicArmTalk","GLOBAL",5)
	SetGlobalTimer("_bVicAerieArm","GLOBAL",ONE_HOUR)
END

IF
	IfValidForPartyDialog("Aerie") 
	IfValidForPartyDialog("Viconia")
	Global("_bAerieVicArmTalk","GLOBAL",5)
	GlobalTimerExpired("_bVicAerieArm","GLOBAL")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bAerieVicArmTalk","GLOBAL",6)
END

IF
	Global("_bAerieVicArmTalk","GLOBAL",6)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//4th Talk Timer
IF
	InParty("Aerie") 
	InParty("Viconia")
	Global("_bAerieRefuseVic","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("_bAerieRefuseVic","GLOBAL",2)
	SetGlobalTimer("_bVicAerieArm3","GLOBAL",TWO_DAYS)
END

IF
	IfValidForPartyDialog("Aerie") 
	IfValidForPartyDialog("Viconia")
	Global("_bAerieRefuseVic","GLOBAL",2)
	GlobalTimerExpired("_bVicAerieArm3","GLOBAL")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bAerieRefuseVic","GLOBAL",3)
END

IF
	Global("_bAerieRefuseVic","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//5th Viconia Talk Trigger
IF
	Global("_bAerieRefuseVic","GLOBAL",4)
	IfValidForPartyDialog("Aerie") 
	IfValidForPartyDialog("Viconia")
	PartyRested()
	CombatCounter(0)
THEN
	RESPONSE #100
	GiveItemCreate("_bWINGV","Viconia",0,0,0)
	XEquipItem("_bWINGV","Viconia",SLOT_HELMET,EQUIP)
	SetGlobal("_bAerieRefuseVic","GLOBAL",5)
END

IF
	Global("_bAerieRefuseVic","GLOBAL",5)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Trigger for Jaheira-Aerie wing talk (global set in jaheira)
IF
	Global("_bAerieJahWingTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Trigger for TOB Aerie-Wing CN Talk
IF
	Global("_bEdwinAerieWings","GLOBAL",17)
	InParty("Aerie") 
	InParty("Edwin")
THEN
	RESPONSE #100
	SetGlobalTimer("_bAWingTimer3","GLOBAL",EIGHT_HOURS)
	SetGlobal("_bEdwinAerieWings","GLOBAL",18)
END

IF
	IfValidForPartyDialog("Aerie") 
	InParty("Edwin")
	Global("_bEdwinAerieWings","GLOBAL",18)
	GlobalTimerExpired("_bAWingTimer3","GLOBAL")
	!PartyHasItem("_BWINGA")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bEdwinAerieWings","GLOBAL",19)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",19)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Trigger for last Edwin Talks
IF
	Global("_bEdwinAerieWings","GLOBAL",20)
	IfValidForPartyDialog("Aerie")
	IfValidForPartyDialog("Edwin")
	!PartyHasItem("_BWINGA")
	OR(2)
	Global("_bAerieDeclinesEdwin","GLOBAL",1)
	Global("_bAerieAgreesEdwin","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bEdwinAerieWings","GLOBAL",21)
END

IF
	Global("_bEdwinAerieWings","GLOBAL",21)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Trigger for Pre-Cutscene Talk
IF
	Global("_bAerieAgreesEdwin","GLOBAL",2)
	PartyRested()
	IfValidForPartyDialog("Aerie") 
	IfValidForPartyDialog("Edwin")
	CombatCounter(0)
	!PartyHasItem("_BWINGA")
	CombatCounter(0)
THEN
	RESPONSE #100
	Wait(1)
	SetGlobal("_bAerieAgreesEdwin","GLOBAL",3)
END

IF
	Global("_bAerieAgreesEdwin","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

	
//Aerie recurring Wing Talk TImer
IF
	Global("_bAerieWingTalk","GLOBAL",0)
	IfValidForPartyDialog("Aerie")
	HaveKnownSpell(CLERIC_REGENERATE)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bAerieWingTimer","GLOBAL",FIVE_DAYS)
	SetGlobal("_bAerieWingTalk","GLOBAL",1)
END

IF
	GlobalTimerExpired("_bAerieWingTimer","GLOBAL")
	Global("_bAerieWingTalk","GLOBAL",1)
	IfValidForPartyDialog("Aerie")
THEN 
RESPONSE #100
	SetGlobal("_bAerieWingTalk","GLOBAL",7)
	SetGlobalTimer("_bAerieWingTimer","GLOBAL",FIFTEEN_MINUTES)
END

IF
	GlobalTimerExpired("_bAerieWingTimer","GLOBAL")
	Global("_bAerieWingTalk","GLOBAL",7)
	IfValidForPartyDialog("Aerie")
	!AreaType(DUNGEON)
	!PartyHasItem("_BWINGA")
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bAerieWingTalk","GLOBAL",2)
	StartDialogNoSet(Player1)
END

IF
	Global("_bAerieWingTalk","GLOBAL",2)
THEN 
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//11th Aerie Talk - once has wings
IF
	PartyHasItem("_BWINGA")
	IfValidForPartyDialog("Aerie")
	Global("_bAerieTOBWing","GLOBAL",1)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bAerieWingTimer","GLOBAL",ONE_DAY)
	SetGlobal("_bAerieTOBWing","GLOBAL",2)
END

IF 
	GlobalTimerExpired("_bAerieWingTimer","GLOBAL")
	Global("_bAerieTOBWing","GLOBAL",2)
	IfValidForPartyDialog("Aerie")
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bAerieTOBWing","GLOBAL",3)
END

IF
	Global("_bAerieTOBWing","GLOBAL",3)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
	

//Aerie post Wing success Talk (actually happens before 11th Talk)
IF
	Global("_bAerieTOBWing","GLOBAL",0)	
	PartyHasItem("_BWINGA")
	IfValidForPartyDialog("Aerie")
	CombatCounter(0)
THEN 
RESPONSE #100
	StartDialogNoSet(Player1)
END



//Aerie-Jan Wings Wizard Talk (1st trigger set in Jan)
IF
	Global("_bJASpellTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Aerie-Neera Wings Wizard Talk (1st trigger set in NEERA)
IF
	Global("_bNeeraASpellTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Aerie Wizard Talk - next trigger in Imoen
IF
	IfValidForPartyDialog("Imoen2")
	IfValidForPartyDialog("Aerie") 
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	!PartyHasItem("_BWINGA")
	Global("_b25SpellHelpsAerie","GLOBAL",0)
	Global("_bImoenSaysPolymorph","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bImoenSaysPolymorph","GLOBAL",1)
END

//Aerie-CN Wizard Talk
IF
	Class(Player1,MAGE_ALL)
	IfValidForPartyDialog("Aerie") 
	HaveKnownSpell(WIZARD_POLYMORPH_SELF)
	Global("_bACNSpellTalk","GLOBAL",0)
	!PartyHasItem("_BWINGA")
	Global("_b25SpellHelpsAerie","GLOBAL",0)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bACNSpellTalk","GLOBAL",1)
END

IF
	Global("_bACNSpellTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Nalia Wizard Talk - 1st trigger in Nalia
IF
	Global("_bNaliaSpellTalk","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Post Lazarus Wings Talk
IF
	IfValidForPartyDialog("Aerie") 
	Global("_b25SpellHelpsAerie","GLOBAL",3)
	PartyHasItem("_BWINGA")
THEN
	RESPONSE #100
	SetGlobal("_b25SpellHelpsAerie","GLOBAL",4)
END

IF
	IfValidForPartyDialog("Aerie") 
	Global("_b25SpellHelpsAerie","GLOBAL",4)
	PartyHasItem("_BWINGA")
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Final Lazarus Line
IF
	IfValidForPartyDialog("Aerie") 
	Global("_b25SpellHelpsAerie","GLOBAL",5)
THEN
	RESPONSE #100
	RestParty()
	SetGlobal("_b25SpellHelpsAerie","GLOBAL",6)
END

IF
	Global("_b25SpellHelpsAerie","GLOBAL",6)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END


//Dorn Conflict Combat Script
IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	InParty("Aerie")
	Global("_bAerieDornFight","GLOBAL",1)
THEN
	RESPONSE #100
	ChangeAIScript("",DEFAULT)
	SetLeavePartyDialogFile() 
	LeaveParty()
	DialogInterrupt(FALSE)
	RunAwayFrom("Dorn",15)  // Dorn
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	HaveSpell(WIZARD_IMPROVED_ALACRITY)  
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_IMPROVED_ALACRITY) 
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	HaveSpell(WIZARD_TIME_STOP)  // SPWI909.SPL (Time Stop)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_TIME_STOP) 
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,SANCTUARY)
	HaveSpell(WIZARD_BIGBYS_CRUSHING_HAND)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_BIGBYS_CRUSHING_HAND) 
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_IMPROVED_MANTLE)  
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_IMPROVED_MANTLE) 
END

IF
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_MANTLE)  
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_MANTLE) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,SANCTUARY)
	HaveSpell(CLERIC_FLAME_STRIKE) 
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_FLAME_STRIKE) 
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,SANCTUARY)
	HaveSpell(CLERIC_BOLT_OF_GLORY) 
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_BOLT_OF_GLORY) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,SANCTUARY)
	HaveSpell(WIZARD_FLAME_ARROW) 
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_FLAME_ARROW) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_MELF_ACID_ARROW)  
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,SANCTUARY)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_MELF_ACID_ARROW) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,SANCTUARY)
	!CheckSpellState("Dorn",MALISON)
	HaveSpell(WIZARD_GREATER_MALISON) 
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_GREATER_MALISON) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!CheckSpellState(Myself,STONESKIN) 
	!StateCheck(Myself,STATE_SILENCED)
	HaveSpell(WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,ARMOR_OF_FAITH)
	HaveSpell(CLERIC_ARMOR_OF_FAITH) 
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_ARMOR_OF_FAITH) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_HASTE)  
	!StateCheck(Myself,STATE_SILENCED)
	!StateCheck(Myself,STATE_HASTED)
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_HASTE) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_MAGIC_MISSILE)  
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,SANCTUARY)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_MAGIC_MISSILE) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HPPercentLT(Myself,75)
	HaveSpell(WIZARD_LARLOCH_MINOR_DRAIN)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,SANCTUARY)
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_LARLOCH_MINOR_DRAIN)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_VOCALIZE)  // SPWI219.SPL (Vocalize)
	StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_VOCALIZE)  // SPWI219.SPL (Vocalize)
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Range("Dorn",10)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_FIRE_SHIELD_BLUE)
	!CheckSpellState(Myself,BLUE_FIRESHIELD)
	!CheckSpellState(Myself,RED_FIRESHIELD) 
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_FIRE_SHIELD_BLUE) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Range("Dorn",10)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_FIRE_SHIELD_RED)  // SPWI418.SPL (Fire Shield (Red))
	!CheckSpellState(Myself,BLUE_FIRESHIELD)
	!CheckSpellState(Myself,RED_FIRESHIELD)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_FIRE_SHIELD_RED)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	Range("Dorn",7)
	!HaveSpell(CLERIC_SANCTUARY)
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100
	RunAwayFrom("Dorn",10)  // Dorn
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	Range("Dorn",7)
	HaveSpell(CLERIC_SANCTUARY)
	CheckStat(Myself,0,SANCTUARY)
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_SANCTUARY)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	OR(2)
	!Range("Dorn",7)
	CheckSpellState(Myself,SANCTUARY) 
	HPPercentLT(Myself,50)
	!StateCheck(Myself,STATE_SILENCED)
	HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)  
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_CURE_MEDIUM_WOUNDS)  
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	OR(2)
	!Range("Dorn",7)
	CheckSpellState(Myself,SANCTUARY) 
	HPPercentLT(Myself,50)
	!StateCheck(Myself,STATE_SILENCED)
	HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)  
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_CURE_SERIOUS_WOUNDS)  
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	OR(2)
	!Range("Dorn",7)
	CheckSpellState(Myself,SANCTUARY) 
	HPPercentLT(Myself,10)
	!StateCheck(Myself,STATE_SILENCED)
	HaveSpell(CLERIC_HEAL)  // SPPR607.SPL (Heal)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_HEAL)  // SPPR607.SPL (Heal)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_SLOW_POISON)  
	StateCheck(Myself,STATE_POISONED)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_SLOW_POISON) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_NEUTRALIZE_POISON)  
	StateCheck(Myself,STATE_POISONED)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_NEUTRALIZE_POISON) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_FINGER_OF_DEATH)  
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_FINGER_OF_DEATH) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_IMPROVED_HASTE)  
	!StateCheck(Myself,STATE_HASTED)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_IMPROVED_HASTE) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!StateCheck(Myself,STATE_INVISIBLE)	
	!StateCheck(Myself,STATE_SILENCED)
	HaveSpell(WIZARD_IMPROVED_INVISIBILITY)  
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_IMPROVED_INVISIBILITY) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_SLOW)  
	!StateCheck("Dorn",STATE_SLOWED)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_SLOW)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_MIRROR_IMAGE)  // SPWI212.SPL (Mirror Image)
	!StateCheck(Myself,STATE_SILENCED)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_MIRROR_IMAGE)  // SPWI212.SPL (Mirror Image)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(4)
	!Range("Dorn",7)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_HOLD_MONSTER)  
	!StateCheck("Dorn",STATE_HELPLESS)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_HOLD_MONSTER)  
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(WIZARD_EMOTION_HOPELESSNESS)  
	!StateCheck("Dorn",STATE_HELPLESS)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",WIZARD_EMOTION_HOPELESSNESS)  
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(4)
	!Range("Dorn",7)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_HOLD_PERSON)  // SPPR208.SPL (Hold Person)
	!StateCheck("Dorn",STATE_HELPLESS)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_HOLD_PERSON)  // SPPR208.SPL (Hold Person)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_SLAY_LIVING) 
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_SLAY_LIVING) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_POISON)  
	!StateCheck("Dorn",STATE_POISONED)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_POISON) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!StateCheck(Myself,STATE_INVISIBLE)	
	HaveSpell(WIZARD_INVISIBILITY)  
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_INVISIBILITY) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_GREATER_COMMAND) 
	!StateCheck("Dorn",STATE_HELPLESS)
	!StateCheck("Dorn",STATE_SLEEPING)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_GREATER_COMMAND) 
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_CAUSE_SERIOUS_WOUNDS)  // SPPR510.SPL (Cause Critical Wounds)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_CAUSE_SERIOUS_WOUNDS)  // SPPR510.SPL (Cause Critical Wounds)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,TENSERS_TRANSFORMATION)
	HaveSpell(WIZARD_TENSERS_TRANSFORMATION)  
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,WIZARD_TENSERS_TRANSFORMATION) 
	AttackReevaluate("Dorn",45)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_CAUSE_CRITICAL_WOUNDS)  // SPPR510.SPL (Cause Critical Wounds)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_CAUSE_CRITICAL_WOUNDS)  // SPPR510.SPL (Cause Critical Wounds)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(5)
	!Range("Dorn",7)
	StateCheck("Dorn",STATE_HELPLESS)
	!CheckSpellState(Myself,MANTLE)
	!CheckSpellState(Myself,IMPROVED_MANTLE)
	!CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
	Global("_bAerieDornFight","GLOBAL",1)
	HaveSpell(CLERIC_HARM)  // SPPR608.SPL (Harm)
	!StateCheck(Myself,STATE_SILENCED)
	RandomNum(3,1)
THEN
	RESPONSE #100
	Spell("Dorn",CLERIC_HARM)  // SPPR608.SPL (Harm)
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	!StateCheck(Myself,STATE_BLESS)
	HaveSpell(CLERIC_BLESS) 
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_BLESS) 
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	!StateCheck(Myself,STATE_CHANT)
	HaveSpell(CLERIC_CHANT) 
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_CHANT) 
	Continue()
END

IF
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	Global("_bAerieDornFight","GLOBAL",1)
	!StateCheck(Myself,STATE_SILENCED)
	!StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
	HaveSpell(CLERIC_DRAW_UPON_HOLY_MIGHT) 
	RandomNum(2,1)
THEN
	RESPONSE #100
	Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT) 
	Continue()
END

IF //Reset after casting spells
	Global("_bAerieDornFight","GLOBAL",1)
	!Range("Dorn",7)
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	!HasItemEquiped("SHLD24","Dorn")  // Reflection Shield +1
	!HasItemEquiped("D1REFSHD","Dorn")  // Reflection Shield +3 (modded)
	!HasItemEquiped("SHLD24A","Dorn")  // Reflection Shield +1 (small version)
THEN
	RESPONSE #100
	EquipRanged()
	AttackReevaluate("Dorn",20)
END

IF
	Global("_bAerieDornFight","GLOBAL",1)
	Range("Dorn",7)
	!Dead("Aerie")  // Aerie
	!Dead("Dorn")  // Dorn
	OR(3)
	!HasItemEquiped("SHLD24","Dorn")  // Reflection Shield +1
	!HasItemEquiped("D1REFSHD","Dorn")  // Reflection Shield +3 (modded)
	!HasItemEquiped("SHLD24A","Dorn")  // Reflection Shield +1 (small version)
THEN
	RESPONSE #100
	EquipMostDamagingMelee()
	AttackReevaluate("Dorn",20)
END

//To start talk if she wins
IF
	Dead("Dorn")  // Dorn
	CombatCounter(0)
	Global("_bAerieDornFight","GLOBAL",1)
THEN
	RESPONSE #100
	JoinParty()
	SetGlobal("_bAerieDornFight","GLOBAL",2)
END

IF
	Global("_bAerieDornFight","GLOBAL",2)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Breakup Talk
IF
	Global("AerieRomanceActive","GLOBAL",3)
	Global("_bMazNotAerie","GLOBAL",1)
	InParty("Aerie") 
	InParty("Mazzy")
THEN
	RESPONSE #100
	SetGlobalTimer("_bAerieBreakUp","GLOBAL",NINE_HOURS)
	SetGlobal("_bMazNotAerie","GLOBAL",2)
END

IF
	GlobalTimerExpired("_bAerieBreakUp","GLOBAL") 
	Global("_bMazNotAerie","GLOBAL",2)
	IfValidForPartyDialogue("Aerie")
	IfValidForPartyDialogue("Mazzy")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bMazNotAerie","GLOBAL",3)
END

IF
	Global("_bMazNotAerie","GLOBAL",3)
THEN
	RESPONSE #100
	PlaySong(0)
	PlaySound("_bMaz1")
	StartDialogNoSet(Player1)
END
