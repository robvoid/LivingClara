IF	//Trigger for HaerDalis leaving the party forever.
	Global("_bHDLeavesForever","GLOBAL",1)
THEN
	RESPONSE #100
	SetGlobal("KickedOut","LOCALS",2)
	GivePartyAllEquipment()
	LeaveParty() 
	EscapeArea()
END

//ClaraSex scene trigger
IF
	InParty("HaerDalis") 
	!StateCheck("HaerDalis",CD_STATE_NOTVALID) 
	InParty("_bClara")
	!StateCheck("_bClara",CD_STATE_NOTVALID)
	Global("_bSWCH","GLOBAL",0)
	Global("LS_HaerDalisSex","GLOBAL",2) 
	!Global("LS_HaerDalisRomanceActive","GLOBAL",3) 
	Global("_bSleepsWithClara","GLOBAL",4)
THEN
	RESPONSE #100
	ActionOverride(Player1,SetDialog("Player1"))
	StartDialogNoSet(Player1)
END

//Dorn-Aerie Conflict Trigger (Here because it's HD's local), the Dorn first version
IF 
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	IfValidForPartyDialog("Aerie")
	GlobalGT("_bDornHDLove","GLOBAL",12)  //Have gotten to at least the 4th talk
	!Global("_bHDDORNROMANCE","GLOBAL",3)  //Romance not turned off
	GlobalGT("LoveTalk","LOCALS",11)   //Have gotten this far in the Aerie-HD love talks
	!Global("HaerDalisRomanceActive","GLOBAL",1)
	!Global("HaerDalisRomanceActive","GLOBAL",2)
	Global("_bAerieDornConflict","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDAerieTimer","GLOBAL",FIVE_HOURS)
	IncrementGlobal("_bAerieDornConflict","GLOBAL",1)
END

IF  //The Aerie first version
	!Global("DornRomanceActive","GLOBAL",1) 
	!Global("DornRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",2)
	!Global("LS_HaerDalisRomanceActive","GLOBAL",1)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	IfValidForPartyDialog("Aerie")
	GlobalGT("_bDornHDLove","GLOBAL",10)  //Have gotten to at least the 3rd talk
	!Global("_bHDDORNROMANCE","GLOBAL",3)  //Romance not turned off
	OR(2)
	Global("HaerDalisRomanceActive","GLOBAL",1)  //Aerie & HD are committed
	Global("HaerDalisRomanceActive","GLOBAL",2)
	Global("_bAerieDornConflict","GLOBAL",0)
THEN 
	RESPONSE #100
	SetGlobalTimer("_bDHDAerieTimer","GLOBAL",FIVE_HOURS)
	IncrementGlobal("_bAerieDornConflict","GLOBAL",1)
END


//Trigger for Post-Rest Dorn Talk (1st trigger in Dorn b/c easier)
IF
	Global("_bDornHDLove","GLOBAL",22)
THEN
	RESPONSE #100
	PlaySong(0)   
	PlaySong(87)  
	StartDialogNoSet(Player1)
END

//Romance Combat Talk - Dorn Injured
IF
	Global("_bHDDORNROMANCE","GLOBAL",2)
	HPPercentLT("Dorn",25)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	Global("_bDornHurt","GLOBAL",0)
	OR(2)
	Global("_bHDHurt","GLOBAL",0)
	Global("_bHDHurt","GLOBAL",3)
	ActuallyInCombat()
THEN 
	RESPONSE #100
	SetGlobal("_bDornHurt","GLOBAL",1)
END

IF
	Global("_bDornHurt","GLOBAL",1)
	HPPercentLT("Dorn",25)
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	OR(2)
	Global("_bHDHurt","GLOBAL",0)
	Global("_bHDHurt","GLOBAL",3)
	ActuallyInCombat()
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF
	Global("_bDornHurt","GLOBAL",2)
	CombatCounter(0)
THEN 
	RESPONSE #100
	SetGlobal("_bDornHurt","GLOBAL",3)
END	

//Joining if Dorn is fighting CN
IF
	!InParty("Dorn")
	Allegiance("Dorn",ENEMY)
	Detect("Dorn")
	Global("_bHDJoinsDorn","GLOBAL",0)
	Global("_bHDDORNROMANCE","GLOBAL",2)
	IfValidForPartyDialog("HaerDalis")
THEN
	RESPONSE #100
	SetGlobal("_bHDJoinsDorn","GLOBAL",1)
END

IF
	Global("_bHDJoinsDorn","GLOBAL",1)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Next Trigger for Helmite Camp Talk (1st trigger in Dorn)
IF
	OR(2) Global("_bHDDORNROMANCE","GLOBAL",1) //starting
	Global("_bHDDORNROMANCE","GLOBAL",2) //romance active.
	IfValidForPartyDialog("Dorn")
	IfValidForPartyDialog("HaerDalis")
	!Detect("OHDHAMMS") //Don't see Helmite ambush
	!FallenPaladin("Dorn")  //Dorn has a patron - demon/Talos/Umberlee
	Global("_bHelmiteTalk","GLOBAL",1)
	GlobalTimerExpired("_bHelmiteTimer","GLOBAL")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bHelmiteTalk","GLOBAL",2)
END

IF
	Global("_bHelmiteTalk","GLOBAL",2) 
THEN
	RESPONSE #100
	PlaySong(0)  
	PlaySong(87)  //Dorn Romance music
	StartDialogNoSet(Player1)
END
