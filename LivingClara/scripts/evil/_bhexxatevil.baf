//Korgan - Hexxat

//1st talk waking conversation trigger:
IF
	Global("_BKorHex","GLOBAL",3)
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

IF //3rd Talk timer - 1st two in Korgan. Outdoors or town near wilderness
	InParty("Korgan")
	InParty("HEXXAT")
	Global("_BKorHex","GLOBAL",7)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",8)
	SetGlobalTimer("_bkorganhextimer3","GLOBAL",THREE_DAYS)
END

IF
	Global("_BKorHex","GLOBAL",8)
	GlobalTimerExpired("_bkorganhextimer3","GLOBAL")
	IfValidForPartyDialogue("Korgan")
	IfValidForPartyDialogue("HEXXAT")
	PartyRested()
	OR (5)
		AreaType(OUTDOOR)
		AreaCheck("AR1105")        // Umar Hills - Imnesvale Inn
		AreaCheck("AR1602")        // Brynnlaw - Brynnlaw Inn
		AreaCheck("AR2010")        // Trademeet - Vytori's Pub
		AreaCheck("AR0021")        // City Gates - Crooked Crane 1st floor
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("_BKorHex","GLOBAL",9)
END

IF
	Global("_BKorHex","GLOBAL",9)
THEN
	RESPONSE #100
	PlaySong(90)
	StartDialogNoSet(Player1)
END

//4th talk timer, town/inn or outdoors, interrupted rest (next trigger in hexxatd)
IF 
	InParty("Korgan")
	InParty("HEXXAT")
	Global("_BKorHex","GLOBAL",10)
	!Global("HexxatRomanceActive","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",11)
	SetGlobalTimer("_bkorganhextimer4","GLOBAL",FOUR_DAYS)
END

//4th talk (previous script in Hexxatd
IF
	Global("_BKorHex","GLOBAL",12)
	AreaType(OUTDOOR)
THEN
	RESPONSE #100
	StartMovie("rest")
	DayNight(MIDNIGHT)
	SetGlobal("_BKorHex","GLOBAL",13)
END

IF
	Global("_BKorHex","GLOBAL",12)
	!AreaType(OUTDOOR)
THEN
	RESPONSE #100
	StartMovie("restinn")
	DayNight(MIDNIGHT)
	SetGlobal("_BKorHex","GLOBAL",13)
END

IF
	Global("_BKorHex","GLOBAL",13)
THEN
	RESPONSE #100
	PlaySong(90)
	StartDialogNoSet(Player1)
END

//waking dialogue - 4th talk
IF
	Global("_BKorHex","GLOBAL",14) 
	CombatCounter(0)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//5th Talk Timer- triggers outdoors/inn not in Athkatla, interrupted
IF 
	InParty("Korgan")
	InParty("HEXXAT")
	Global("_BKorHex","GLOBAL",15)
	!Global("HexxatRomanceActive","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",16)
	SetGlobalTimer("_bkorganhextimer5","GLOBAL",THREE_DAYS)
END

//previous trigger in Hexxatd
IF
	Global("_BKorHex","GLOBAL",17)
	AreaType(OUTDOOR)
THEN
	RESPONSE #100
	StartMovie("rest")
	SetGlobal("_BKorHex","GLOBAL",18)
END

IF
	Global("_BKorHex","GLOBAL",17)
	!AreaType(OUTDOOR)
THEN
	RESPONSE #100
	StartMovie("restinn")
	SetGlobal("_BKorHex","GLOBAL",18)
END

IF
	Global("_BKorHex","GLOBAL",18)
THEN
	RESPONSE #100
	PlaySong(90)
	StartDialogNoSet(Player1)
END

//Last Talk
IF 
	InParty("Korgan")
	InParty("HEXXAT")
	Global("_bKorganHexxatFriends","GLOBAL",1)
	Global("_BKorHex","GLOBAL",19)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",20)
	SetGlobalTimer("_bkorganhextimer6","GLOBAL",FIVE_DAYS)
END

IF
	Global("_BKorHex","GLOBAL",20)
	GlobalTimerExpired("_bkorganhextimer6","GLOBAL")
	IfValidForPartyDialogue("Korgan")
	IfValidForPartyDialogue("HEXXAT")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",21)
END

IF
	Global("_BKorHex","GLOBAL",21)
THEN
	RESPONSE #100
	PlaySong(90)
	StartDialogNoSet(Player1)
END

//3rd Viconia-Hexxat Talk trigger (post death)
IF
	IfValidForPartyDialogue("Viconia")
	IfValidForPartyDialogue("HEXXAT")
	Global("_bHexVic","GLOBAL",6)
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bHexVic","GLOBAL",7)
END

IF
	Global("_bHexVic","GLOBAL",7)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//4th Talk, after next rest
IF
	IfValidForPartyDialogue("Viconia")
	IfValidForPartyDialogue("HEXXAT")
	Global("_bHexVic","GLOBAL",8)
	PartyRested()
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_bHexVic","GLOBAL",9)
END

IF
	Global("_bHexVic","GLOBAL",9)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END

//Trigger for 1st Hexxat-Bodhi talk
IF
	Global("_bCloakTalk","GLOBAL",1)
	InParty("_BBodhi")
	!StateCheck("_BBodhi",CD_STATE_NOTVALID)
	InParty("HEXXAT")
	!StateCheck("HEXXAT",CD_STATE_NOTVALID)
THEN
	RESPONSE #100
	StartDialogNoSet(Player1)
END
