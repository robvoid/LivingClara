IF //Start of Korgan/Hexxat Timer Trigger. - next script in _bkorgandevil
	InParty("Korgan")
	InParty("Hexxat")
	Global("OHH_korgan","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",1)
	!Global("HexxatRomanceActive","GLOBAL",2)
	Global("_BKorHex","GLOBAL",0)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",1)
	SetGlobalTimer("_bkorganhextimer1","GLOBAL",TWO_DAYS)
END

//First Korgan-Hexxat conversation, previous trigger in d script. Dialogue in _bKorganHexevil.d (soa only).  Won't trigger in dungeons, all else okay
IF
	Global("_BKorHex","GLOBAL",2)
THEN
	RESPONSE #100
	PlaySong(90)
	StartDialogNoSet(Player1)
END

IF //2nd Talk timer - Won't trigger in dungeons or Ust Natha all else okay
	InParty("Korgan")
	InParty("Hexxat")
	Global("_BKorHex","GLOBAL",4)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",5)
	SetGlobalTimer("_bkorganhextimer2","GLOBAL",FIVE_DAYS)
END

IF
	Global("_BKorHex","GLOBAL",5)
	GlobalTimerExpired("_bkorganhextimer2","GLOBAL")
	!AreaType(DUNGEON)
	!AreaCheck("AR2200")
	IfValidForPartyDialogue("Korgan")
	IfValidForPartyDialogue("Hexxat")
	CombatCounter(0)
THEN
	RESPONSE #100
	SetGlobal("_BKorHex","GLOBAL",6)
END

IF
	Global("_BKorHex","GLOBAL",6)
THEN
	RESPONSE #100
	PlaySong(90)
	StartDialogNoSet(Player1)
END


